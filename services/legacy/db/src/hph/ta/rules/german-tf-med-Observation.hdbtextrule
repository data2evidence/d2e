!======================================================================
! File Name: german-tf-med-Observation.rul
! Author: ALP Text Analysis
! Project Name: Medical Fact Extraction
! Content: Rules for the extraction of med_Observation
!======================================================================

#subgroup NumModifier:{(
<unter>
|<über>
)}

#subgroup NumRange:{(
(<bis> <POS:Num>)
|(<(\<)|(\>)> <POS:Num>)
|(<\(>? <\->? <POS:Num>)
)}



#subgroup PercentRange:{(
(%(NumModifier)|<POS:Num>)? [TE PERCENT] <>+ [/TE]
)}

!========================
! Link
!========================

#subgroup Link:{(
(<\:>)
|(<am> <>?)
|(<von>)
)}

#subgroup LabTestLink:{(
(<\(> <(ISE)|(neu)> <\)>)
|(<(Anstieg)|(gestiegen)> <auf>)
|((<von>|<STEM:sein, POS:Aux-Fin>) <>{0,3})
|(<verbesser.*> <sich> <vo(n|m)> <POS:Num> <>? <auf>)
|(<konstant> <bei>)
|(<\/Liquor>)
|(<quant\.>)
|(<\:>)
)}

!=========================
! PSType
!=========================

#subgroup PSType:{(
[OD Observation_PerformanceStatus](
[TE OBSERVATION_PERFORMANCESTATUS] <>+ [/TE]
) [/OD]
)}

!==========================
! PSValue
!==========================

#subgroup PSValue:{(
[OD Observation_PerformanceStatus_Value] (%(NumRange)|%(PercentRange)) [/OD]
)}

!==========================
! PSPair
!==========================

#subgroup PSPair:{(
( %(PSType) %(Link)? %(PSValue) )
)}

!=========================
! BMIType
!=========================

#subgroup BMIType:{(
[OD Observation_BMI](
<BMI>
|<KMI>
|<Körper((gewichts)|(masse))index>
) [/OD]
)}

!=========================
! BMIValue
!=========================

#subgroup BMIValue:{(
[OD Observation_BMI_Value] %(NumRange) [/OD]
)}

!=========================
! BMIPair
!=========================

#subgroup BMIPair:{(
%(BMIType) %(Link)? %(BMIValue)
)}

!==========================
! WeightType
!==========================

#subgroup WeightType:{(
[OD Observation_BodyWeight](
<(Körper)?(G|g)ewicht>
)[/OD]
)}

!==========================
! WeightValue
!==========================

#subgroup WeightUnit:{(
[OD Observation_BodyWeight_Unit](
<\p{ci}(kg(\.)?)>
)[/OD]
)}

#subgroup WeightValue:{(
[OD Observation_BodyWeight_Value] <[0-9]+(kg(\.)?)?> [/OD]
)}

!==========================
! WeightPair
!==========================

#subgroup WeightPair:{(
%(WeightType) %(Link)? %(WeightValue) %(WeightUnit)?
)}

!===========================
! HeightType
!===========================

#subgroup HeightType:{(
[OD Observation_BodyHeight](
<(Körper)?(G|g)rö((ss)|ß)e>
) [/OD]
)}

!===========================
! HeightValue
!===========================

#subgroup HeightUnit:{(
[OD Observation_BodyHeight_Unit](
  <(c?m)|((Zenti)?meter)>
) [/OD]
)}

#subgroup HeightValue:{(
[OD Observation_BodyHeight_Value] <[0-9]+(c?m)?>|%(NumRange) [/OD]
)}

!============================
! HeightPair
!============================

#subgroup HeightPair:{(
( %(HeightType) %(Link)? %(HeightValue) %(HeightUnit)? )
)}

!============================
! TempType
!============================

#subgroup TempType:{(
[OD Observation_BodyTemperature](
(<(Körper)?(T|t)emperatur>)
|(<Fieber>) 
)[/OD]
)}

!============================
! TempValue
!============================

#subgroup TempUnit:{(
[OD Observation_BodyTemperature_Unit](
<\p{ci} ((°c)|(c)|(celsius))>
) [/OD]
)}

#subgroup TempValue:{(
[OD Observation_BodyTemperature_Value] <[0-9]+°(c|C)>|%(NumRange) [/OD]
)}

!==============================
! TempPair
!==============================

#subgroup TempPair: {(
( %(TempType) %(Link)? %(TempValue) %(TempUnit)? )
)}

!============================
! BPType
!============================

#subgroup BPType:{(
([OD Observation_BloodPressure](
<BP>
|<Blutdruck>
|<BD>
) [/OD])
|([OD Observation_BloodPressure_Component_Systolic]( <systolisch.*> <Blutdruck> )[/OD])
|([OD Observation_BloodPressure_Component_Diastolic]( <diastolisch.*> <Blutdruck> )[/OD])
)}

#subgroup BPTypePost:{(
([OD Observation_BloodPressure_Component_Systolic] <\p{ci}(systolisch)> [/OD])
|([OD Observation_BloodPressure_Component_Diastolic] <\p{ci}(diastolisch)> [/OD])
)}

!============================
! BPValue
!============================

#subgroup BPUnit:{(
[OD Observation_BloodPressure_Unit](
(<mmHg>)
|(<mm> <Hg>)
)[/OD]
)}

#subgroup BPValue:{(
[OD Observation_BloodPressure_Value] (
 %(NumRange)
) [/OD]
)}

!===========================
! BPPair
!===========================

#subgroup BPPair:{(
( %(BPType) %(Link)? %(BPValue) %(BPUnit)? )
|([OD Observation_BloodPressure_Value]  <[0-9]([0-9]|\-)+> [/OD] %(BPUnit)?  %(BPTypePost))
)}

!============================
! PulseType
!============================

#subgroup PulseType:{(
[OD Observation_HeartRate] (
<Puls>
|<HF>
) [/OD]
)}

!============================
! PulseValue
!============================

#subgroup PulseUnit:{(
[OD Observation_HeartRate_Unit](
<\/(min)>
) [/OD]
)}

#subgroup PulseValue:{(
[OD Observation_HeartRate_Value](
<[0-9]+(\/(min))?>
)[/OD]
)}

!=============================
! PulsePair
!=============================

#subgroup PulsePair:{(
( %(PulseType) %(Link)? %(PulseValue) %(PulseUnit)? )
)}

!=============================
! SatType
!=============================

#subgroup SatType:{(
[OD Observation_OxygenSaturation](
<Sauerstoffsättigung>
|<(Sa)?O2>
)[/OD]
)}

!==============================
! SatValue
!==============================

#subgroup SatValue:{(
[OD Observation_OxygenSaturation_Value] %(PercentRange) [/OD]
)}

!=============================
! SatPair
!=============================

#subgroup SatPair:{(
%(SatType) <>{0,3} %(SatValue)
)}

!=============================
! RespType
!=============================

#subgroup RespType:{
[OD Observation_RespirationRate](
<RR>
|<Atemfrequenz>
) [/OD]
}

!=============================
! RespValue
!=============================

#subgroup RespUnit:{(
[OD Observation_RespirationRate_Unit](
<mmHg>
)[/OD]
)}

#subgroup RespValue:{(
[OD Observation_RespirationRate_Value] %(NumRange) [/OD]
)}

!==============================
! RespPair
!==============================

#subgroup RespPair:{(
( %(RespType) %(Link)? %(RespValue) %(RespUnit)? )
)}

!==============================
! LabTestType
!==============================

#subgroup LabTestType:{(
([OD Observation_Laboratory_Normalized] [TE OBSERVATION_LABORATORY]<>+[/TE] [/OD])
)}

!==============================
! LabTestValue
!==============================

#subgroup LabTestUnit:{(
[OD Observation_Laboratory_Unit](
(<(m|µ)mol\/(L|l)>)
|(<(m|µ)?g\/d?l>)
|(<((n?g)|(l))\/l>)
|(<(k|m)?U\/(m)?l>)
|(<\/pl>)
|(<\/nl>)
|(<fl>)
|(<pg>)
|(<s>)
) [/OD]
)}

#subgroup LabTestValue:{(
([OD Observation_Laboratory_Value] %(NumRange) [/OD])
)}

!===============================
! LabTestPair
!===============================

#subgroup LabTestPair:{(
%(LabTestType) %(LabTestLink)? %(LabTestValue) <L>? %(NumRange)? %(LabTestUnit)?
)}

!=============================
! med_Observation
!=============================

#group Observation: {(
%(PulsePair)
|%(HeightPair)
|%(WeightPair)
|%(SatPair)
|%(BMIPair)
|%(RespPair)
|%(BPPair)
|%(TempPair)
|%(PSPair)
|%(LabTestPair)
)}

#group DROP_Observation:{(
(<K(i|I)> <67(\%)?> <>{0,3})
|(<K(I|i)\-67> <\:>? <>{0,3})
)}
