!======================================================================
! File Name: french-tf-med-QuantitativeConcept.rul
! Author: Pierre Marchal (PI IC HC Platform FR)
! Project Name: Medical Fact Extraction
! Content: Rules for the extraction of quantitative concepts
!======================================================================



!======================================================================

!! BODY WEIGHT

!======================================================================

#define BW_separator: [=\:\/\-\,]
#define BW_unit: \p{ci}(kgs?|kilos?|kilogrammes?|k|gs?|grs?|grammes?)
#define BW_integer: [0-9]{1,4}
#define BW_real: [0-9]{1,4}([\.\,][0-9]{1,3})

#subgroup BW_COPULE: {
    (<qui>? <STEM:se>? <STEM:(être|rester|maintenir|conserver|estimer|évaluer)>)? <\p{ci}(à|a)>? <de>?
}

#subgroup BW_KEYWORD_MODIFIER: {
    <\,,POS:Punct>?(
    <POS:Prep-de> <STEM:(entrée|sortie)>
    | <POS:(Prep-de|Prep-a)>? <POS:Adv>
    | (<STEM:(s[tr]able|diminuer|stabiliser|stabilisé)> <\p{ci}(à|a)>? | <\p{di}(stableà)>)
    | <ce> <jour> 
    | <POS:Adj,STEM:(actuel|conservé|habituel|ajusté|faible|initial|normal)>
    | <POS:Det> <POS:Adv> <STEM:bas>
    | <POS:Prep-a> <POS:Adj-Pre-Sg> ! au long de...
    | <POS:Prep,STEM:~(pour)> <POS:(Det|Num)>? <POS:Nn>
    | <\p{ci}(à|a)> <\p{ci}(jeun)>
    | <\p{ci}(à|a)> <\p{ci}j\+?[0-9]+>
    | <en> <\p{ci}(janvier|f[eé]vrier|mar|avril|mai|juin|juillet|ao[ûu]t|septembre|octobre|novembre|d[eé]cembre)> <([12])?[0-9]{3}>?
    | <\p{di}(tout\-à\-fait)>
    | (<en>|<sans><>?) <progrès>
    | (<un> <peu> | <POS:Adv>)? <en> <POS:Adj>? <STEM:(augmentation|diminution|baisse)>
    | <il> <y>? <a> <[0-9]+> <\p{ci}(jour|mois)>
    | <le> <[0-9]{1,2}\/[0-9]{1,2}>
    | <STEM:(avant|après)> <POS:Det>? <POS:Nn>
    )
}

#subgroup BODY_HEIGHT: {
    (<\p{ci}(taille|t)> <%(BW_separator)>? )? (
    <[0-2]> <\p{ci}(m)> <[0-9]{1,2}>
    | <[0-2]\p{ci}(m)[0-9]{1,2}>
    | <[0-2][\.\,][0-9]{1,2}> <\p{ci}(m)>
    | <[0-2][\.\,][0-9]{1,2}\p{ci}(m)>
    | <[0-9]{2,3}([\.\,][0-9])?> <cm>
    | <[0-9]{2,3}cm>
    )
}

#subgroup BW_APRES_KEYWORD: {
    <%(BW_separator)>?
    ( %(BW_COPULE) | %(BW_KEYWORD_MODIFIER) | <\(> %(BW_KEYWORD_MODIFIER) <\)>)*
    <%(BW_separator)>?
}

#subgroup BW_KEYWORD: {
    [OD Observation_BodyWeight] (
    [TE WEIGHT@KEYWORD] <>+ [/TE] | <ne> [TE WEIGHT@KEYWORD] <POS:V> [/TE] <que>
    ) [/OD]
}

#subgroup BW_VALUE: {
    [OD Observation_BodyWeight_Value] ( <%(BW_integer)> | <%(BW_real)> ) [/OD]
}

#subgroup BW_UNIT :{
    [OD Observation_BodyWeight_Unit]
    <\p{ci}(k|kgs?|kilos?|kilogrammes?)>
    | <\p{ci}(gs?|grs?|grammes?)> 
    [/OD]
}

#subgroup BW_NON_TOKENIZED_VALUE_UNIT: {
    <%(BW_real)%(BW_unit)>
    | <%(BW_integer)%(BW_unit)>
    | <%(BW_integer)%(BW_unit)%(BW_integer)>
}

#subgroup BW_VALUE_UNIT: {
    [OD Observation_BodyWeight_Value] <%(BW_integer)> [/OD] %(BW_UNIT) <%(BW_integer)>?
    | [OD Observation_BodyWeight_Value] <%(BW_real)> [/OD] %(BW_UNIT)
    | [OD Observation_BodyWeight_Value] %(BW_NON_TOKENIZED_VALUE_UNIT) [/OD] 
}

#subgroup BW_AVANT_VALUE: {
    <STEM:être> (
        <POS:Prep> <STEM:(dessus|dessous)>
        | <STEM:(monter|descendre)> (<STEM:jusqu'>| <POS:Prep> <STEM:(dessus|dessous)> )?
    ) <POS:Prep>
}

#subgroup BW_IDIOM: {
    !! KEYWORD + VALUE_UNIT
    [OD Observation_BodyWeight] (
        <\p{ci}(état|etat)> <\p{di}(général)> <POS:Adj> <\:>
        | <\p{ci}(examen)> <\p{ci}(clinique)> <\:>
    ) [/OD]
    %(BW_VALUE_UNIT)
    !! VALUE_UNIT + KEYWORD
    | %(BW_VALUE_UNIT) [OD Observation_BodyWeight] <en> <baseline> [/OD]
}

#subgroup BODY_WEIGHT: {
    ( %(BW_KEYWORD) %(BW_APRES_KEYWORD) ( %(BW_VALUE_UNIT) | <\(> %(BW_VALUE_UNIT) <\)> )
        | %(BW_AVANT_VALUE) %(BW_VALUE_UNIT) ) 
    | %(BODY_HEIGHT) (<%(BW_separator)> | <pour>)?  %(BW_VALUE_UNIT)
    | %(BW_VALUE_UNIT) (<%(BW_separator)> | <pour>)? %(BODY_HEIGHT) 
    | %(BW_KEYWORD) <\(> %(BW_UNIT) <\)> %(BW_APRES_KEYWORD) <[=\:]> ( %(BW_VALUE) | [OD Observation_BodyWeight_Value] %(BW_NON_TOKENIZED_VALUE_UNIT) [/OD] )
    | <STEM:(passer|passé|passage)>
        <de> ((<%(BW_real)>|<%(BW_integer)>) <%(BW_unit)>? |%(BW_NON_TOKENIZED_VALUE_UNIT))
        <à|a> %(BW_VALUE_UNIT)
    | %(BW_VALUE_UNIT) <\(> ( <[\-\+]> | <\p{di}(était)> <\p{di}(à)> ) ( <%(BW_integer)> | <%(BW_real)> ) 
    | %(BW_IDIOM)
}


!======================================================================

!! PERFORMANCE STATUS

!======================================================================

#subgroup PS_MODIFIEUR: {
    <POS:Adj> <\p{di}(à)> <STEM:évaluer>
}

#subgroup PS_CIRCONSTANT_AVANT: {
    <avant> <POS:Det>? <POS:Nn>
    | <POS:Prep> <POS:Det>? <STEM:entrée>
}

#subgroup PS_CIRCONSTANT_APRES: {
    <après> <POS:Det>? <POS:Nn>
    | <POS:Prep> <POS:Det>? <STEM:sortie>
}

#subgroup PS_CIRCONSTANT: {
    <sous> <STEM:traitement>
    | <lors> <POS:Prep>? <POS:Det> <STEM:examen>
    | <au> <diagnostic>
    | <ce> <jour>
    | <(le|au)> <3[01]|[12][0-9]|0?[1-9]> <[\-\.\/]>? <1[012]|0?[1-9]> <[\-\.\/]>? <(19|20)?[0-9]{2}>
    | <(le|au)> <(3[01]|[12][0-9]|0?[1-9])[\-\.\/](1[012]|0?[1-9])[\-\.\/]((19|20)?[0-9]{2})>
}

#subgroup PS_COPULE: {
    <[=/\:\-\+]>
    | ( <POS:Pron-IntRel>? <STEM:être>)? <POS:Adv>? <POS:(Adj|V/Adj),STEM:~(passé)>?
        ( <de> (<l'> <ordre> <de> )?
        | (<aux> <alentours>| <autour>) <de> 
        | <\p{di}(à)> )
    | ( <POS:Pron-IntRel>? <STEM:être>)? <STEM:rester>? <POS:(Adv|Adj),STEM:~(passé)> <\p{di}(à)>?
    | ( <POS:Conj-que> <POS:Pron> | <POS:Pron-IntRel>? <STEM:pouvoir>? <STEM:être>)?
        <STEM:(évaluer|chiffrer|estimer|côté)> <\p{di}(à)>
    | <STEM:(être|rester)>
}

#subgroup PS_MISC : <["\,]>
#subgroup TE_AMBIGUOUS_PS: [TE OBSERVATION_PERFORMANCESTATUS] <\p{ci}(le)>? <\p{ci}(ps)> [/TE]
#subgroup TE_ECOG: [TE OBSERVATION_PERFORMANCESTATUS@ECOG] <>+ [/TE]
#subgroup TE_KARNOFSKY: [TE OBSERVATION_PERFORMANCESTATUS@KARNOFSKY] <>+ [/TE]
#subgroup TE_VALUE_ECOG: [TE PS__VALUE@ECOG] <>+ [/TE]
#subgroup OD_VALUE_ECOG: [OD Observation_PerformanceStatus_Value] [TE PS__VALUE@ECOG] <>+ [/TE] [/OD]
#subgroup TE_VALUE_KARNOFSKY: [TE PS__VALUE@KARNOFSKY] <>+ [/TE]
#subgroup OD_VALUE_KARNOFSKY: [OD Observation_PerformanceStatus_Value] [TE PS__VALUE@KARNOFSKY] <>+ [/TE] [/OD]

#subgroup SCORING_SYSTEM_ECOG : {
    [OD Observation_PerformanceStatus] %(TE_ECOG) [/OD] ( <\(> %(TE_ECOG) <\)> )?
    (<POS:Prep-de> <POS:Det>? <STEM:patient>
    | <POS:Adj> )?
}

#subgroup VALUE_ECOG: {
    %(PS_MISC)?
    (
    %(OD_VALUE_ECOG)
    | %(OD_VALUE_ECOG) <voire?> %(OD_VALUE_ECOG)
    | <de>? %(OD_VALUE_ECOG) <\p{di}(à)> %(OD_VALUE_ECOG)
    | (<STEM:être> <comprise?>)? <entre> %(OD_VALUE_ECOG) <et> %(OD_VALUE_ECOG)
    | %(OD_VALUE_ECOG) (<STEM:tendre> <STEM:vers>|<STEM:limite>) %(OD_VALUE_ECOG)
    | %(OD_VALUE_ECOG) %(PS_MISC)? <STEM:tendance> <\p{di}(à)>? %(OD_VALUE_ECOG)
    )
    %(PS_MISC)?
}

#subgroup COMPLEX_VALUE_ECOG: {
    %(PS_MISC)?
    ( <STEM:être> <STEM:passé> <de> %(TE_VALUE_ECOG) <\p{di}(à)> %(OD_VALUE_ECOG)
    | %(TE_VALUE_ECOG) %(PS_MISC)? <mais>
        ( %(PS_CIRCONSTANT) | %(PS_CIRCONSTANT_APRES) | %(PS_COPULE) )+ 
        %(OD_VALUE_ECOG) )
}

#subgroup PS_ECOG: {
    %(SCORING_SYSTEM_ECOG)
    ( %(PS_COPULE) | %(PS_MODIFIEUR) | %(PS_CIRCONSTANT) | %(PS_CIRCONSTANT_AVANT) | %(PS_CIRCONSTANT_APRES) | %(PS_MISC) )*
    ( %(VALUE_ECOG) | %(COMPLEX_VALUE_ECOG) )
}

#subgroup SCORING_SYSTEM_KARNOFSKY : {
    ( [OD Observation_PerformanceStatus] %(TE_KARNOFSKY) [/OD]
    | ( %(TE_AMBIGUOUS_PS) | %(TE_KARNOFSKY) ) <\(> [OD Observation_PerformanceStatus] %(TE_KARNOFSKY) [/OD] <\)>
    | [OD Observation_PerformanceStatus] %(TE_KARNOFSKY) [/OD] <\(> ( %(TE_AMBIGUOUS_PS) | %(TE_KARNOFSKY) ) <\)> )
    (<POS:Prep-de> <POS:Det>? <STEM:patient>)?
}

#subgroup VALUE_KARNOFSKY: {
    %(PS_MISC)?
    (
    %(OD_VALUE_KARNOFSKY)
    | %(OD_VALUE_KARNOFSKY) <voire?> %(OD_VALUE_KARNOFSKY)
    | <de>? %(OD_VALUE_KARNOFSKY) <\p{di}(à)> %(OD_VALUE_KARNOFSKY)
    | <entre> %(OD_VALUE_KARNOFSKY) <et> %(OD_VALUE_KARNOFSKY)
    )
    %(PS_MISC)?
}

#subgroup COMPLEX_VALUE_KARNOFSKY: {
    %(PS_MISC)? %(PS_CIRCONSTANT_AVANT) %(TE_VALUE_KARNOFSKY)
    %(PS_MISC)? %(PS_CIRCONSTANT_APRES) %(OD_VALUE_KARNOFSKY)

}

#subgroup PS_KARNOFSKY : {
    %(SCORING_SYSTEM_KARNOFSKY)
    ( %(PS_MODIFIEUR) | %(PS_CIRCONSTANT) | %(PS_MISC) )*
    %(PS_COPULE)?
    ( %(VALUE_KARNOFSKY) | %(COMPLEX_VALUE_KARNOFSKY) )
}

#subgroup PERFORMANCE_STATUS: {
    %(PS_KARNOFSKY)
    | %(PS_ECOG)
}


!======================================================================

!! MAIN

!======================================================================

#group Observation: {
    %(PERFORMANCE_STATUS)
    | %(BODY_WEIGHT)
}


