!======================================================================
! File Name: french-tf-med-Pathology.hdbtextrul
! Author: Pierre Marchal (PI IC HC Platform FR)
! Project Name: Medical Fact Extraction
! Content: Rules for the extraction of pathology report specific entities
!======================================================================


!======================================================================
! OUTPUT FIELDS:
!======================================================================

! Main cancer group: med_CancerHistology

!The following subentity must be present to trigger the rule:
!!CancerHistology

!Optional subentities:
!!CancerBehavior
!!GradeType
!!GradeValue
!!CancerSite


! Peripheral entities:
!       1) med_CancerGrade;
!       2) med_CancerSize;
!       3) med_CancerStage;
!======================================================================





!======================================================================
! SUB-RULES
!======================================================================

#include "hc.hph.ta.rules::common.hdbtextinclude"


!======================================================================
! FINAL RULE
!======================================================================

#subgroup OD_Observation_CancerStage: {
    ! T? N? M
    ( [OD Observation_CancerStage_Component_T] <%(tnm_t)> [/OD] %(parentheses)? )?
        ( [OD Observation_CancerStage_Component_N] <%(tnm_n)> [/OD] %(parentheses)? )?
        [OD Observation_CancerStage_Component_M] <%(tnm_m)> [/OD] 
    ! T? N M?
    | ( [OD Observation_CancerStage_Component_T] <%(tnm_t)> [/OD] %(parentheses)? )?
        [OD Observation_CancerStage_Component_N] <%(tnm_n)> [/OD] 
        ( %(parentheses)? [OD Observation_CancerStage_Component_M] <%(tnm_m)> [/OD] )?
    ! T N? M?
    | [OD Observation_CancerStage_Component_T] <%(tnm_t)> [/OD] 
        ( %(parentheses)? [OD Observation_CancerStage_Component_N] <%(tnm_n)> [/OD] )?
        ( %(parentheses)? [OD Observation_CancerStage_Component_M] <%(tnm_m)> [/OD] )?
    | [OD Observation_CancerStage_Value_TNMComplete] <%(tnm_t)%(tnm_n)%(tnm_m)> [/OD]
}

#group Observation_CancerStage: %(OD_Observation_CancerStage)


#group DROP_Observation_CancerStage: {
    <STEM:(axial|coronal|sagittal|volumique)> %(OD_Observation_CancerStage)
    | <STEM:(pondération|séquence|isosignal|hypersignal)> %(OD_Observation_CancerStage)
    | %(OD_Observation_CancerStage) <\p{ci}(fat|sat|fat/sat)>
    | <\p{ci}(fat|sat|fat/sat)> %(OD_Observation_CancerStage)
}

!!!!!!!!!!!!!!!!!!
!! CANCER GRADE


#group Observation_CancerGrade: {
    <POS:Adv>? %(OD_CancerGradeValue) 
    | %(OD_CancerGradeType) ( <\:> | <\p{di}(à)> )? %(OD_CancerGradeValue_ambiguous)
    | %(OD_CancerHistology) <POS:Prep>?
        [OD Observation_CancerGrade_Value] [TE OBSERVATION_CANCERGRADE_VALUE] <grade.*> <>* [/TE] [/OD]
}




!!!!!!!!!!!!!!!!!
!! CANCER SIZE

#group Observation_CancerSize: {
    %(OD_CancerGradeValue) %(histology__size) %(OD_CancerSize)
    | %(OD_CancerGradeValue) %(histology_modifier) %(histology__size) %(OD_CancerSize)
    | %(OD_CancerHistology) %(histology__size) %(OD_CancerSize)
    | %(size_keyword) %(OD_CancerSite) %(OD_CancerSize)
    | %(size_keyword) %(OD_CancerSite) %(site__size) %(OD_CancerSize)
    | %(OD_CancerSite) %(site__size) %(OD_CancerSize)
    | %(size_keyword) %(histology__size) %(OD_CancerSize)
    | %(OD_CancerHistology) %(OD_CancerBehavior) %(histology__size) %(OD_CancerSize)
    | %(size_keyword) %(OD_CancerSize)
    | %(OD_CancerGradeValue) %(OD_CancerSize)
    | %(OD_CancerHistology) %(OD_CancerSize)
    | %(OD_CancerSite) %(OD_CancerSize)
}


