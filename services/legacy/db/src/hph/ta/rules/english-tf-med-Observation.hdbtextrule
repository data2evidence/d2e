!======================================================================
! File Name: english-tf-med-Observation.rul 
! Author: ALP Text Analysis
! Project Name: Medical Fact Extraction
! Content: Rules for the extraction of Observation
!======================================================================
!======================================================================
! DEPENDENCIES:
! > DICTIONARY english-tf-med-LabTest-NCIm
! >>>> ENTITY_NAME med_LAB_TEST@NCIm
! > DICTIONARY english-tf-med-Observation
! >>>> ENTITY_NAME Observation_PerformanceStatus
!======================================================================
!======================================================================
! OUTPUT:
! > ENTITY Observation
! >>>> SUBENTITY Type <BMI, BodyWeight, BodyHeight, BodyTemp, BP, 
!      BPSystolic, BPDiastolic, HeartRate, 
!      OxygenSat, RespRate, PerformanceStatus, LabTest> {1}
! >>>> SUBENTITY Value {1,2}
! >>>> SUEBTNTIY unit {0,2}
!======================================================================
!======================================================================
! GeneralValue
!======================================================================

#subgroup MaxMin: {(
  (<(at|AT|At)>? <\p{ci}(max|min|maximum|minimum)>)
| (<(at|AT|At)> <(LEAST|Least|least|Most|MOST|most)>)
)}

#subgroup NumModifier: {(
  <(ABOUT|About|about|approximately|Approximately|APPROXIMATELY|Above|ABOVE|above|Below|BELOW|below)> 
| <(UP|up|Up)> <(TO|to|To)> 
| <(AS|As|as)> <\p{ci}(high|low|much|little|many|few)> <(AS|As|as)>
| <\p{ci}(higher|greater|more|lower|smaller|less)> <\p{ci}(than)>
| <(\>|\<)>
| %(MaxMin)
)}

#subgroup NumRange: {(
  ( %(NumModifier)? <POS:Num> <STEM: thousand>?)
| ( <POS:Num> %(MaxMin))
| <[0-9]+\+>
| ( (<STEM:\p{ci}(range)> <\p{ci}(from)>)? <POS:Num> <\p{ci}(to|and|\-)> <POS:Num>)
| ( <POS:Prep>? <the|THE|The> <\p{ci}(high|low)>? <POS:Num> <'s|'S>)
| (<[0-9]+s([\-\/][0-9]+s)?>)
| (<[0-9]+s\/(lower|mid|upper)> <[0-9]+s>)
| (<[0-9]+> <'> <STEM: s> (<STEM: to> <[0-9]+> <'> <STEM: s>)?)
)}

#subgroup PercentRange: {(
  ( %(NumModifier)? [TE PERCENT] <>+ [/TE] )
| ([TE PERCENT] <>+ [/TE] %(MaxMin))
| (([TE PERCENT] <>+ [/TE]|<POS:Num>) <TO|To|to|\-> [TE PERCENT] <>+ [/TE] )
| (<\p{ci}(between)> ([TE PERCENT] <>+ [/TE]|<POS:Num>) <\p{ci}(and)> [TE PERCENT] <>+ [/TE])
)}

#subgroup GeneralValue: {(
(%(NumRange) |  %(PercentRange) )
)}

!======================================================================
! Link
!======================================================================

#subgroup Link: {(
  (<STEM: at> <POS: Det> <STEM: point>)?
  (
  <(WAS|WERE|was|were|IS|is|ARE|are|\:|\-\-?|=|equal|equals)>
| (<STEM:be>? (<POS: V, STEM:record|note|range|vary|rate|rating|run|running>|<POS: V|Nn, STEM: ranging>)? <\p{ci}(of|at|to|from|between|in)> <POS: Det>?)
| (<STEM:have>? <STEM:((be)|(remains?))>)
| (<STEM: on> <STEM: admission|presentation> <STEM: be>?)
| ((<STEM:be> <STEM: then>? <STEM:find|note|record|test> <to>)? ( <be>
         | (<STEM:have|be>? <POS: Adv>? <STEM:((rise)|(fall)|(increase)|(decrease)|(equal)|(drop)|(rebound)|(elevate)|(change)|(peak)|(nadir)|(bottom)|(improve))> <\p{ci}(at|to|in)>? <POS: Det>?)))
| (<STEM:have>? <STEM:return> <\p{ci}(with|to)>?)
| (<STEM: be> <STEM: then> <STEM: note> <STEM: to> <STEM: be> <STEM: increase> <STEM: to>)
| (<STEM: be>? <STEM: in> <POS: Det>)
)
)}


#subgroup LabTestLink: {(
  ((<STEM: be> <STEM: peak>)? (<STEM: at> [TE TIME_PERIOD] <>* [/TE] <STEM: of> <STEM: life>)|(<STEM: on> <STEM: day> <STEM: of> <STEM: life> <POS: Num>))?
  (
  ( <(WAS|WERE|was|were|IS|is|ARE|are|\:|\-\-?|=|Of|of|OF|At|at|AT|To|TO|to)>)
| (<STEM: currently|today|show>)
| (<POS: Prep> [TE DATE] <>* [/TE] <STEM: be>)
| (<STEM: on> <STEM: admission|presentation> <STEM: be>?)
| (<STEM:be>? <STEM:record|note|range|ranging|up> <\p{ci}(of|at|to|from)>)
| (<STEM:have>? <STEM:((be)|(remains?))>)
| ((<STEM:be> <STEM:find|note|record|test> <to>)? ( <be>
         | (<STEM: be|have>? <POS: Adv>? <STEM:((rise)|(fall)|(increase)|(decrease)|(equal)|(drop)|(rebound)|(elevate)|(change)|(peak)|(nadir)|(bottom)|(normal)|(low)|(stable)|(down)|(improve)|(increase)|(suppress))> <\p{ci}(at|to|with)>?)))
| (<STEM:have>? <STEM:return> <\p{ci}(with|to)>?)
| (<\(> [TE OBSERVATION_LABORATORY] <[A-Za-z]{2,3}> [/TE] <\)>)
  )
)}


!======================================================================
! PSType
!======================================================================

#subgroup PSType: {(
[OD Observation_PerformanceStatus] (
[TE OBSERVATION_PERFORMANCESTATUS] <>+ [/TE]
) [/OD]
)}


!======================================================================
! PSValue
!======================================================================

#subgroup PSValue: {(
[OD Observation_PerformanceStatus_Value] (%(NumRange) | %(PercentRange)) [/OD]
)}


!======================================================================
! PSPair
!======================================================================

#subgroup PSPair: {(
   ( %(PSType) %(Link)? %(PSValue))
)}

!======================================================================
! BMIType
!======================================================================

#subgroup BMIType: {(
[OD Observation_BMI] (
 (<\p{ci}(BMI)>)
|(<\p{ci}(body)><\p{ci}(mass)>)
|(<\p{ci}(quetelet)>)
) <\p{ci}(index)>? [/OD]
)}


!======================================================================
! BMIValue
!======================================================================

#subgroup BMIValue: {(
[OD Observation_BMI_Value] %(NumRange) [/OD]
)}


!======================================================================
! BMIPair
!======================================================================

#subgroup BMIPair: {(
   ( %(BMIType) %(Link)? %(BMIValue))
)}


!======================================================================
! WeightType
!======================================================================

#subgroup WeightType: {(
[OD Observation_BodyWeight] (
<STEM:\p{ci}(weights?|weighs?|Wt)>
) [/OD]
)}


!======================================================================
! WeightValue
!======================================================================

#subgroup WeightUnit: {(
[OD Observation_BodyWeight_Unit] ( 
  <\p{ci}(kgs?\.?|Kgs?\.?|kilos?|(kilo)?grams?|gms?\.?)>
| <\p{ci}(ounces?|ozs?\.?|pounds?|lbs?\.?)>
| <\p{ci}(percentile)>
) [/OD]
)}


#subgroup WeightValue: {(
  [OD Observation_BodyWeight_Value] %(GeneralValue)[/OD]
)}

!======================================================================
! WeightPair
!======================================================================

#subgroup WeightPair: {(
( %(WeightType) %(Link)? %(WeightValue) %(WeightUnit)? (%(WeightValue) %(WeightUnit))?)
| ((%(WeightValue) %(WeightUnit)?){1,2} [OD Observation_BodyWeight] <STEM: (fe)?male>? <STEM: infant> [/OD])
)}



!======================================================================
! HeightType
!======================================================================

#subgroup HeightType: {(
[OD Observation_BodyHeight] (
(<\p{ci}(height|Ht)>)
|(<STEM: (fe)?male> <STEM: infant|patient>? <measuring>)
) [/OD]
)}

#subgroup HeightTypePost: {(
[OD Observation_BodyHeight] (
<\p{ci}(tall)>
) [/OD]
)}


!======================================================================
! HeightValue
!======================================================================

#subgroup HeightUnit: {(
[OD Observation_BodyHeight_Unit] ( 
  <\p{ci}(c?m\.?|(centi)?meters?|(centi)?metres?)>
| <\p{ci}(inch(es)?|in\.?|foot|feet|'|"|ft\.?)>
| <\p{ci}(percentile)>
) [/OD]
)}

#subgroup HeightValue: {(
  [OD Observation_BodyHeight_Value] %(GeneralValue)[/OD]
| [OD Observation_BodyHeight_Value] (<[0-9]+'([0-9]+")?> | <[0-9]+"> ) [/OD]
)}

!======================================================================
! HeightPair
!======================================================================

#subgroup HeightPair: {(
( %(HeightType) %(Link)? (%(HeightValue) %(HeightUnit)?){1,2})
|(%(HeightType) %(Link)? %(HeightValue) %(HeightUnit) (<POS: Conj>? %(HeightValue) %(HeightUnit)?)?)
|(%(HeightValue) %(HeightUnit) ((<\,>|<POS: Conj>)? %(HeightValue) %(HeightUnit)?)?  %(HeightTypePost))
)}


!======================================================================
! TempType
!======================================================================

#subgroup TempType: {(
[OD Observation_BodyTemperature] (
 <STEM:\p{ci}(temp|temperature|t\-?max|t\-?current)> <STEM: current>?
|(<T> <\p{ci}(max|current)>)
|(<Tc>)
|<\p{ci}(a?febrile)>
|<\p{ci}(fever)>
) [/OD]
)}


!======================================================================
! TempValue
!======================================================================

#subgroup TempUnit: {(
[OD Observation_BodyTemperature_Unit] ( 
  <\p{ci}(°c|c|celsius|°f|f|fahrenheit|degrees?)> <\p{ci}(degrees?|celsius|fahrenheit)>?
) [/OD]
)}

#subgroup TempValue: {(
[OD Observation_BodyTemperature_Value]  (%(NumRange)|<[0-9]{1,3}(\.[0-9]{1,2})?°?[CF]>) [/OD]
)}

!======================================================================
! TempPair
!======================================================================

#subgroup TempPair: {(
( %(TempType) %(Link)? %(TempValue) %(TempUnit)? )
)}

!======================================================================
! BPType
!======================================================================

#subgroup BPType: {(
([OD Observation_BloodPressure] (
<\p{ci}(BP|PCWP|RVEDP)>
|(<\p{ci}(blood)> <\p{ci}(pressure)>)
|<\p{ci}(hypertensive|hypotensive)>
) [/OD]
)
(<\p{ci}(on|in)> <\p{ci}(the)> <\p{ci}(right|left)> <\p{ci}(arm)>? )?
)}


#subgroup BPDType: {(
 ([OD Observation_BloodPressure_Component_Diastolic]( 
(<\p{ci}(diastolic)> <\p{ci}(blood)> <\p{ci}(pressure)>) 
|( <\p{ci}(blood)> <\p{ci}(pressure)> <\p{ci}(diastolic)>) 
|<\p{ci}(DBP)> 
) [/OD])
(<\p{ci}(on|in)> <\p{ci}(the)> <\p{ci}(right|left)> <\p{ci}(arm)>? )?
)}

#subgroup BPSType: {(
([OD Observation_BloodPressure_Component_Systolic]( 
(<\p{ci}(systolic)> <\p{ci}(blood)> <\p{ci}(pressure)>) 
|( <\p{ci}(blood)> <\p{ci}(pressure)> <\p{ci}(systolic)>) 
|<\p{ci}(SBP)> 
) [/OD])
(<\p{ci}(on|in)> <\p{ci}(the)> <\p{ci}(right|left)> <\p{ci}(arm)>? )?
)}

#subgroup BPDTypePost: {(
[OD Observation_BloodPressure_Component_Diastolic] <\p{ci}(diastolic)> [/OD]
)}


#subgroup BPSTypePost: {(
[OD Observation_BloodPressure_Component_Systolic] <\p{ci}(systolic)> [/OD]
)}


!======================================================================
! BPValue
!======================================================================

#subgroup BPUnit: {(
  <\p{ci}(mmHg)>
| (<\p{ci}(mm)> <\p{ci}(Hg)>)
)}

#subgroup BPValue: {(
  %(NumRange)  
| ( %(NumRange)  <\p{ci}(over|\/)> %(NumRange)  )
| ((<[0-9]{2,3}\/P>)|(<[0-9]{2,3}> <\/> <(P|palpable)>))
)}

!======================================================================
! BPPair
!======================================================================


#subgroup BPDPair: {(
( %(BPDType) %(Link)?  [OD Observation_BloodPressure_Component_Diastolic_Value] %(BPValue)[/OD]  ([OD Observation_BloodPressure_Component_Diastolic_Unit] %(BPUnit) [/OD] )? )
|([OD Observation_BloodPressure_Component_Diastolic_Value]  <[0-9]([0-9]|\-)+> [/OD] ([OD Observation_BloodPressure_Component_Diastolic_Unit] %(BPUnit) [/OD] )?  %(BPDTypePost))
)}

#subgroup BPSPair: {(
( %(BPSType) %(Link)?  [OD Observation_BloodPressure_Component_Systolic_Value] %(BPValue)[/OD]  ([OD Observation_BloodPressure_Component_Systolic_Unit] %(BPUnit) [/OD] )? )
|([OD Observation_BloodPressure_Component_Systolic_Value]  <[0-9]([0-9]|\-)+> [/OD] ([OD Observation_BloodPressure_Component_Systolic_Unit] %(BPUnit) [/OD] )?  %(BPSTypePost))
)}


#subgroup BPPair: {(
( %(BPType) %(Link)? [OD Observation_BloodPressure_Value]%(BPValue)[/OD] ([OD Observation_BloodPressure_Unit] %(BPUnit) [/OD])? )
)}

!======================================================================
! PulseType
!======================================================================

#subgroup PulseType: {(
[OD Observation_HeartRate] (
 <\p{ci}(pulse)>
|<\p{ci}(HR)>
|(<\p{ci}(pulse|heart)> <\p{ci}(rate)>)
|<\p{ci}((tachy|brady)cardi[ac])>
) [/OD]
)}


!======================================================================
! PulseValue
!======================================================================

#subgroup PulseUnit: {(
[OD Observation_HeartRate_Unit] (
  (<\p{ci}(beats?)> (<\p{ci}(per)><\p{ci}(minute)>)?)
| <\p{ci}(bpm\.?)>
| (<\p{ci}(per)><\p{ci}(minute)>)
) [/OD]
)}

#subgroup PulseValue: {(
[OD Observation_HeartRate_Value] %(NumRange)  [/OD]
)}

!======================================================================
! PulsePair
!======================================================================

#subgroup PulsePair: {(
( %(PulseType) %(Link)? %(PulseValue) %(PulseUnit)?)
| (%(PulseValue) %(PulseUnit)? <STEM: be> <STEM: his|her|their> %(PulseType))
)}

!======================================================================
! SatType
!======================================================================

#subgroup SatType: {(
[OD Observation_OxygenSaturation] (
 <\p{ci}((O2)?saturations?|sats?|Sa[O0]2|O2Sats?|O2|oxygenation|oxysat)> <\.>? 
|<\p{ci}(saturating|satt?ing|desaturated)>
|(<\p{ci}(O2|oxygen)> <\p{ci}(saturations?|sats?)>)
|(<\p{ci}(pulse)>? <\p{ci}(oximetry)>)
) <\p{ci}(measurement|test)>? [/OD]
(<\p{ci}(on)> (<\p{ci}(ventilation)> | (<\p{ci}(room)> <\p{ci}(air)>) | <[0-9]L(iters?)?> | ((<[0-9]>|<POS: Num>) <\p{ci}(L|liters?|litres?)>) (<STEM: of> <STEM: oxygen|O2>)?) )?
)}

#subgroup SatTypePost: {(
[OD Observation_OxygenSaturation] (
 <\p{ci}(saturated)>
|(<\p{ci}(O2|oxygen)>? <\p{ci}(saturations?|sats?)>)
|(<RA>)
|(<\p{ci}(on)> (<\p{ci}(ventilation)> | (<\p{ci}(room)> <\p{ci}(air)>) | <[0-9]L(iters?)?> | (<[0-9]> <\p{ci}(L|liters?|litres?)> (<STEM: of> <STEM: oxygen|O2>)?)) )
) [/OD]
)}

!======================================================================
! SatValue
!======================================================================

#subgroup SatValue: {(
[OD Observation_OxygenSaturation_Value] %(PercentRange) [/OD]  
)}

!======================================================================
! SatPair
!======================================================================

#subgroup SatPair: {(

( %(SatType) %(Link)? ((%(SatValue))|([OD Observation_OxygenSaturation_Value] (<POS: Num>|<[0-9]+\%\/.+>) [/OD])) )
|( %(SatValue) %(SatTypePost) )
|([OD Observation_OxygenSaturation_Value] <POS: Num> [/OD] <STEM: on> <RA>)
)}


!======================================================================
! RespType
!======================================================================

#subgroup RespType: {(
[OD Observation_RespirationRate] (
 (<\p{ci}(respirations?|breathing)>)
|(<\p{ci}(RR)>)
|(<\p{ci}(respiratory|respiration|resp\.?)> <\p{ci}(rates?)>?)
|(<\p{ci}((tachy|brady)pno?ei[ca])>)
) [/OD]
)}


!======================================================================
! RespValue
!======================================================================

#subgroup RespUnit: {(
[OD Observation_RespirationRate_Unit] (
  (<\p{ci}(breaths?)> (<\p{ci}(per)><\p{ci}(minute)>)?)
| <\p{ci}(insp\/min)>
| <\p{ci}(b?pm\.?)>
| (<\p{ci}(per)><\p{ci}(minute)>)
) [/OD]
)}

#subgroup RespValue: {(
[OD Observation_RespirationRate_Value] %(NumRange) [/OD]
)}

!======================================================================
! RespPair
!======================================================================

#subgroup RespPair: {(
( %(RespType) (%(Link)|<\,>)? %(RespValue) %(RespUnit)? )
)}


!======================================================================
! LabTestType
!======================================================================

#subgroup LabTestType: {(
  ([OD Observation_Laboratory_Normalized] [TE OBSERVATION_LABORATORY]<>+[/TE]  (<STEM: and> [TE OBSERVATION_LABORATORY] <creatinine> [/TE])? [/OD] <\p{ci}(counts?|levels?|sum|total|average|mean|measurements?|tests?|results?|values?|cts?)>?)
| ([OD Observation_Laboratory_Normalized] [TE OBSERVATION_LABORATORY]<>+[/TE] (<STEM: to> [TE OBSERVATION_LABORATORY]<>+[/TE])? [/OD] <STEM: ratio>)
)}


!======================================================================
! LabTestValue
!======================================================================

#subgroup LabTestUnit: {(
[OD Observation_Laboratory_Unit] ( 
  <\p{ci}((mc?|n)?g\.?(\/(m|d|f)?L\.?)?)>
| <\p{ci}(([nm]?mol|meq)\/l)>
| <\p{ci}(mmhg)>
| <\p{ci}([mdf]?l)>
| (<STEM: per> <STEM: ml>)
| (<\p{ci}(m[o0]sm\/(km)?g)>)
| (<\p{ci}((m|n)?g\/(micro|deci|milli|kilo)?lit(re|er))>)
| (<\p{ci}(m\/second)>)
| (<STEM: gram> <\/> <\p{ci}([mdf]?l)>)
| (<\/> <STEM: cmm\.?>)
| (<\p{ci}(ml\/[a-zA-Z]+\/[a-zA-Z]+)>)
)
[/OD]
)}

#subgroup LabTestValue: {(
[OD Observation_Laboratory_Value] %(NumRange) [/OD]
)}

!======================================================================
! LabTestPair
!======================================================================

#subgroup LabTestPair: {(
( %(LabTestType)  %(LabTestLink)? %(LabTestValue) %(LabTestUnit)? )
)}

!======================================================================
! Observation
!======================================================================

#group Observation: {(
   %(PSPair)
|  %(BMIPair)
|  %(WeightPair)
!|  %(HeightPair)
|  %(TempPair)
|  %(BPSPair)
|  %(BPDPair)
|  %(BPPair)
|  %(PulsePair)
|  %(SatPair)
|  %(RespPair)
|  %(LabTestPair)
)}

#group DROP_Observation: {(
  <[A-Z][a-z]+> <(\>|\<)>? [TE TIME_PERIOD] <>* [/TE]
| <>{1,2} <\-[0-9]+(\.[0-9]+)?> <STEM: degree> 
| <T> <\:> [TE DATE] <>* [/TE]
| <SAT> [TE DATE] <>* [/TE]
| %(SatType) <POS: Prep>? <POS: Num> <STEM: liter|litre>
| <POS: Prop> [TE PHONE] <>* [/TE]
| %(LabTestType) %(LabTestLink)? <[MCVIXmcvix]+, POS: Num>
| %(LabTestType) %(LabTestLink)? [TE DATE]<>*[/TE]
| <\p{ci}(PG|PLT)> <POS: Num> <BPX|pack>
)}
