!======================================================================
! File Name: french-tf-med-Assertion.hdbtextrule 
! Author: ALP Text Analysis
! Project Name: Medical Fact Extraction
! Content: Rules for the extraction of assertions of medical entities, 
!          focusing on polarity, modality, reference and relation
!======================================================================


!======================================================================
! OUTPUT FIELDS
!======================================================================

!! Main entity:		sub-entities

!! Polarity: 			1+ sub-entity PolNegative
!! ModalityHypothetical: 	1+ sub-entity ModHypothetical
!! ModalityConditional: 	1+ sub-entity ModConditional
!! ModalityPossible: 		1+ sub-entity ModPossible
!! ModalityProposed: 		1+ sub-entity ModProposed
!! med_ReferenceNotAssociated: 	1+ sub-entity med_RefNotAssociated
!! med_ReferenceFamily: 	1+ sub-entity med_RefFamily
!! med_RelationAllergic: 	1+ sub-entity med_RelAllergic
!! med_RelationIntolerant: 	1+ sub-entity med_RelIntolerant



#subgroup TE_ANATOMY: [TE OBSERVATION_CANCERHISTOLOGY_BODYSITE] <>+ [/TE]
#subgroup TE_med_HISTO: [TE OBSERVATION_CANCERHISTOLOGY_VALUE_ICDO] <>+ [/TE]
#subgroup TE_BIO_TEST: [TE OBSERVATION_CANCERBIOMARKER_METHOD] <>+ [/TE]
#subgroup TE_BIO_ALT: [TE OBSERVATION_CANCERBIOMARKER_COMPONENT_ALTERATION] <>+ [/TE]
#subgroup TE_BIO_GENE: [TE OBSERVATION_CANCERBIOMARKER_NORMALIZED] <>+ [/TE]
#subgroup TE_HISTO_BEHAVIOR: [TE OBSERVATION_CANCERHISTOLOGY_COMPONENT_BEHAVIOR] <>+ [/TE]
#subgroup TE_MEDICATION: [TE MEDICATIONSTATEMENT_MEDICATION] <>+ [/TE]
#subgroup TE_DIS: [TE CONDITION] <>+ [/TE]
#subgroup TE_PROC_D: [TE PROCEDURE_DIAGNOSTIC] <>+ [/TE]
#subgroup TE_PROC_T: [TE PROCEDURE_THERAPEUTIC] <>+ [/TE]
#subgroup TE_LAB_TEST: [TE OBSERVATION_LABORATORY] <>+ [/TE]
#subgroup TE_SYMP: [TE OBSERVATION_SYMPTOM] <>+ [/TE]




!========================================================================
! POLARITY NEGATIVE
!========================================================================

#subgroup PolNegative_keyword_pre: {
      (<\p{ci}(absence)> <POS:Prep>)
    | (<\p{ci}(pas)> <POS:Prep>)
    | (<STEM:aucun>)
    | (<ne> <STEM:retrouver> <pas> <POS:Prep>)
    | (<STEM:interrompre|stopper|arrêter>)
}

#subgroup PolNegative_keyword_post: {
     (<STEM:être> <STEM:interrompre|stopper|arrêter>)
}

#group polarity: {
    (%(PolNegative_keyword_pre) [OD Negative] (%(TE_med_HISTO)|%(TE_DIS)|%(TE_SYMP)) [/OD])
  | (%(PolNegative_keyword_pre) <>{0,2} [OD Negative] (%(TE_MEDICATION)|%(TE_PROC_T)|%(TE_PROC_D)|%(TE_LAB_TEST)) [/OD])
  | ([OD Negative] (%(TE_MEDICATION)|%(TE_PROC_T)|%(TE_PROC_D)|%(TE_LAB_TEST)) [/OD] <>{0,2} %(PolNegative_keyword_post))
}



!========================================================================
! MODALITY POSSIBLE
!========================================================================

#subgroup ModPossible_keyword: {
    <\p{ci}(probablement)> <POS:Adj>?
    | <\p{ci}(compatible)> <avec> <POS:Det>
    | <\p{ci}(recherche)> <de>
    | <STEM:faire> <STEM:évoquer> <POS:Det>
    | <STEM:éventuel>
}


!========================================================================
! MODALITY PROPOSED
!========================================================================

#subgroup ModProposed_keyword: {
    (<STEM:falloir> <POS:Adv>? <POS:V> <POS:Det>)
  | (\p{ci}<dès> \p{ci}<que> \p{ci}<possible>)
  | (\p{ci}<le> \p{ci}<plus> \p{ci}<vite> \p{ci}<possible>)
  | <STEM:proposer>
  | \p{ci}<(aura|auront|passera|passeront)>
  | (\p{ci}<demande> \p{ci}<STEM:de>)
}




!========================================================================
! MODALITY HYPOTHETICAL
!========================================================================

#subgroup ModHypothetical_keyword: {
    (<\p{ci}(si)> \p{ci}<besoin>)
  | (<\p{ci}(en)> \p{ci}<cas> \p{ci}<de>)
  | (<\p{ci}(sb)>)
}



#group status: {
      (%(TE_BIO_ALT) %(ModPossible_keyword) [OD Provisional] %(TE_BIO_GENE) [/OD])
    | (%(ModPossible_keyword) [OD Provisional] (%(TE_med_HISTO)|%(TE_DIS)|%(TE_SYMP)) [/OD])
    | (%(ModPossible_keyword) [OD Provisional] %(TE_BIO_ALT) [/OD])
    | (%(ModPossible_keyword) [OD Provisional] %(TE_HISTO_BEHAVIOR) [/OD])
    | (%(ModPossible_keyword) [OD Provisional] (%(TE_PROC_T)|%(TE_PROC_D)|%(TE_LAB_TEST)) [/OD])
    | (%(ModProposed_keyword) [OD Preparation] %(TE_BIO_TEST) [/OD])
    | (%(ModProposed_keyword) <>{0,2} [OD Preparation] (%(TE_PROC_D)|%(TE_LAB_TEST)|%(TE_PROC_T)) [/OD])
    !!PRIMPERAN 2 fois par jour si besoin
    | ([OD Provisional] %(TE_MEDICATION) [/OD] (<[a-z0-9\/]+>|<[A-Z][a-z0-9\/]+>|<\/>){0,5} %(ModHypothetical_keyword))
}


!========================================================================
! FAMILY REFERENCE
!========================================================================

#subgroup FamilyMember_modifier: {
    <\p{di}(aîné|aînée)>
    | <cadet|cadette>
    | <paternel|paternelle>
    | <maternel|maternelle>
}

#subgroup FamilyMember: {
    <POS:Det> %(FamilyMember_modifier)
    | ( <\p{ci}(m[èe]re)>
        | <\p{ci}(p[èe]re)>
        | <STEM:\p{ci}(fr[èe])re>
        | <STEM:\p{ci}(soeur|sœur)>
        | <STEM:\p{ci}(oncle)>
        | <STEM:\p{ci}(tante)>
        | <STEM:\p{ci}(cousin)>
        | <STEM:\p{ci}(cousine)>
        | <STEM:\p{ci}(grand\-p[èe]re)>
        | <STEM:\p{ci}(grand\-m[èe]re)>
        | <STEM:\p{ci}(neveu)> )
        %(FamilyMember_modifier)?
}

#subgroup reffamily_modifier: {
    <notion> <de>
}

#subgroup OD_RefFamily: {
    %(reffamily_modifier)?
    [OD FamilyMemberHistory] (%(TE_med_HISTO)|%(TE_DIS)|%(TE_SYMP)) [/OD]
}

#subgroup familymember__reffamily: {
    <[\:\,]>
    | (<\:>|<STEM:être>)? <STEM:(décédé|décéder)> ( <à> <l'> <âge> <de> <POS:Num> <ans> )? <\,>? <POS:Prep>? <POS:Det>?
    | <STEM:avoir> <STEM:être> <STEM:(diagnostiqué|diagnostiquer)> <pour> <POS:Det>
}

#group subject: {
    %(FamilyMember) %(OD_RefFamily)
    | %(FamilyMember) %(familymember__reffamily) %(OD_RefFamily)
    | [OD FamilyMemberHistory] ( %(TE_med_HISTO) | %(TE_DIS) | %(TE_SYMP) | %(TE_ANATOMY) ) [/OD] <chez> <POS:Det> %(FamilyMember)
}


!========================================================================
! ALLERGIC RELATION
!========================================================================

#subgroup relAllergic_keyword: {
    <\p{ci}(allergie)>
    | <\p{ci}(allergique)|\p{ci}(allergie)> <POS:Prep> <POS:Det>?
}

#subgroup OD_RelAllergic: {
    [OD AllergyIntolerance_Reaction_Substance] %(TE_MEDICATION) [/OD]
    ( <\,>? <\p{ci}(et)>? ( <POS:Prep> <POS:Det>? )?
        [OD AllergyIntolerance_Reaction_Substance] %(TE_MEDICATION) [/OD] )*
}

#group AllergyIntolerance_Allergy: {
    %(relAllergic_keyword) %(OD_RelAllergic)
}


!========================================================================
! INTOLERANT RELATION
!========================================================================

#subgroup relIntolerant_keyword: {
    <\p{ci}(intol[ée]rance|intol[ée]rante?s?)> <POS:Prep> <POS:Det>?
}

#subgroup OD_RelIntolerant: {
    [OD AllergyIntolerance_Reaction_Substance] %(TE_MEDICATION) [/OD]
    ( <\,>? <\p{ci}(et)>? ( <POS:Prep> <POS:Det>? )?
        [OD AllergyIntolerance_Reaction_Substance] %(TE_MEDICATION) [/OD] )*
}

#group AllergyIntolerance_Intolerance: {
    %(relIntolerant_keyword) %(OD_RelIntolerant)
}







