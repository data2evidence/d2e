!======================================================================
! File Name: german-tf-med-Biomarker.rul 
! Author: ALP Text Analysis
! Project Name: Medical Fact Extraction
! Content: Rules for the extraction of Biomarkers and their relations
!======================================================================

!======================================================================
!! BIOMARKERS:
!======================================================================

!======================================================================
! OUTPUT FIELDS:
!======================================================================

! Biomarker group: med_RelationBiomarkerTestResult

! At least two of the following subentities must be present to trigger the rule:

!!Biomarker
!!BiomarkerStatusEquivocal
!!BiomarkerStatusNegative
!!BiomarkerStatusPositive
!!BiomarkerStatusUnknown
!!BiomarkerTest
!!BiomarkerAlteration
!!BiomarkerAlterationNegative

!Optional subentities:
!!BiomarkerLocation
!!BiomarkerPercent
!!BiomarkerValue

!======================================================================


!======================================================================
!! Common defines:
!======================================================================

#subgroup coord: <\p{ci}((sowie)|(und)|&|(oder)|/|(\|))> | (<\,> <\p{ci}(und|&|oder)>?) | <u\.>

#subgroup skipParens: <\(> <~(\()>+ <\)>

#subgroup PrepOF: {(
  <\p{ci}(vo(m|n))>
)}

#subgroup PrepIN: {(
  <\p{ci}(i(m|n))>
)}

#subgroup PrepFOR: {(
  <\p{ci}(für(s?))>
)}

#subgroup PrepALL: {(
  <POS:Prep>
)}

#subgroup Observed: {(
  <STEM:((sichtbar)|(beobachtet)|(observiert))>
)}

#subgroup LinkingV: {(
 <STEM:((sein)|(werden)), POS:V> 
)}

#subgroup GenitiveArticle: {
  (<eine(s|r)>)
| (<de(s|r)>)
}

!======================================================================
!!Entities coming from dictionaries
!======================================================================

#subgroup DictBiomarker:    [OD Observation_CancerBiomarker_Normalized] [TE OBSERVATION_CANCERBIOMARKER_NORMALIZED] <>+ [/TE] [/OD]
#subgroup DictBiomarkerPos: [OD POS] [TE OBSERVATION_CANCERBIOMARKER_NORMALIZED] <>* <(.*\+)|(.*positiv.*)> [/TE] [/OD]
#subgroup DictBiomarkerNeg: [OD NEG] [TE OBSERVATION_CANCERBIOMARKER_NORMALIZED] <>* <(.*\-)|(.*negativ.*)> [/TE] [/OD]
#subgroup DictBiomarkers:   %(DictBiomarker) (%(coord) %(DictBiomarker))* 
#subgroup DictBiomarkerPar: <\(> %(DictBiomarker) <\)>
#subgroup DictBiomarkerCollection: {
%(DictBiomarker) <bis> <POS:Num>
| %(DictBiomarker) <POS:Num>? (((</>)|(<\,>)|(<und>))? <POS:Num>)+
}


#subgroup Med_Biomarker:    %(DictBiomarker) ( <STEM:((Gen)|(Protein)), POS:Nn>{0,2} | %(DictBiomarkerPar) )+ 
#subgroup Med_Biomarkers:   %(Med_Biomarker) (%(coord) %(Med_Biomarker))*
#subgroup Med_Biomarkers_Pos: %(DictBiomarkerPos) (%(coord) %(DictBiomarkerPos))*
#subgroup Med_Biomarkers_Neg: %(DictBiomarkerNeg) (%(coord) %(DictBiomarkerNeg))* 
#subgroup Med_Test: {
  (%(DictBiomarkers) <>?)? 
  [OD Observation_CancerBiomarker_Method] [TE OBSERVATION_CANCERBIOMARKER_METHOD] <>+ [/TE] [/OD] 
  (<STEM:((für)|(mit)), POS:Prep>? (%(DictBiomarkers)|<POS:Adj|Prop|Num|Nn>+))*
}

!!======================================================================
! Hard-coded entities:
!!======================================================================

#subgroup OtherAlt: {
  ( <(T|t)(ranslokation)?\([0-9][0-9]?;[0-9][0-9]?\)(\(q[0-9][0-9]?;q[0-9][0-9]?(\.[0-9])?\))?> )
| ( <\p{ci}((T|t)(ranslokation))> <\(> <[0-9][0-9]?;[0-9][0-9]?> <\)> (<\(> <\(q[0-9][0-9]?;q[0-9][0-9]?(\.[0-9])?> <\)>)? )
}

#subgroup OtherAlteration: {
  [OD Observation_CancerBiomarker_Component_Alteration] %(OtherAlt) [/OD] %(DictBiomarker)?
}

#subgroup 17pdeletion: { 
  [OD Observation_CancerBiomarker_Component_Alteration]
  ( <\p{ci}(del)> <[1-9][0-9]?(p|q)> )
| ( <\p{ci}(del)[1-9][0-9]?(p|q)> )
| ( <\p{ci}(del)> <\(> <[1-9][0-9]?(p|q)> <\)> )
| ( <\p{ci}(del)\([1-9][0-9]?(p|q)\)> )
| ( <\p{ci}(deletion)[1-9][0-9]?(p|q)> )
| ( <[1-9][0-9]?(p|q)> <\->? <\p{ci}((deletion)|(del)|(syndrom))> )
  [/OD] 
}

#subgroup OtherDeletion: { 
  [OD Observation_CancerBiomarker_Component_Alteration] 
  (
      ( <(X|([1-9][0-9]?))(p|q)[1-9][0-9]((\.|\-)[1-9][0-9]?)?> )
    | ( <[1-9][0-9]?(p|q)[1-9][0-9](\.[1-9][0-9]?)?(\-)(p|q)[1-9][0-9](\.[1-9][0-9]?)?> )
  ) 
  %(skipParens)? 
  <\p{ci}(deletion)>? <\p{ci}(syndrom)>?
  [/OD] 
}

#subgroup Nucleotides: {
  <(T|A|G|C)(T|A|G|C)(T|A|G|C)(T|A|G|C)+>
}


#subgroup DNAnucleotides: {
  ![OD NucleotidesAlteration] 
  ( (<[a-z]\.[0-9]+>) <(T|A|G|C)((T|A|G|C)(T|A|G|C))?\-?\>(T|A|G|C)(T|A|G|C)(T|A|G|C)> )
| ( <[a-z]\.[0-9]+([0-9]|\-|\+|_)*((ins|inv|dup|del)(T|A|G|C)*[0-9]*)+> ) 
| ( <\(?(T|A|G|C)((T|A|G|C)(T|A|G|C))?\-?\>(T|A|G|C)((T|A|G|C)(T|A|G|C))?> <\)>? )
| ( <\(?([a-z]\.[0-9]+)?(T|A|G|C)((T|A|G|C)(T|A|G|C))?\-?\>(T|A|G|C)((T|A|G|C)(T|A|G|C))?\)?> <\)>? )
| %(Nucleotides)
  ![/OD] 
}

#subgroup DictAminoCode: [TE AminoAcid] <>+ [/TE] | <His> | <Met> | <Sec>

#subgroup AminoAcids: {
  ![OD AminoAcidAlteration] 
  <([a-z]\.)?\(?(Leu|Ser|Ile|Glu|Val|Lys|Thr|Phe|Ala|Gly|Arg|Gln|Asp|Asn|Pro|Tyr|Met|His|Trp|Cys|Asx|Sec|Xaa|Glx|LEU|SER|ILE|GLU|VAL|LYS|THR|PHE|ALA|GLY|ARG|GLN|ASP|ASN|PRO|TYR|MET|HIS|TRP|CYS|ASX|SEC|XAA|GLX)[0-9]+(Leu|Ser|Ile|Glu|Val|Lys|Thr|Phe|Ala|Gly|Arg|Gln|Asp|Asn|Pro|Tyr|Met|His|Trp|Cys|Asx|Sec|Xaa|Glx|LEU|SER|ILE|GLU|VAL|LYS|THR|PHE|ALA|GLY|ARG|GLN|ASP|ASN|PRO|TYR|MET|HIS|TRP|CYS|ASX|SEC|XAA|GLX)>
| ( <(Leu|Ser|Ile|Glu|Val|Lys|Thr|Phe|Ala|Gly|Arg|Gln|Asp|Asn|Pro|Tyr|Met|His|Trp|Cys|Asx|Sec|Xaa|Glx|LEU|SER|ILE|GLU|VAL|LYS|THR|PHE|ALA|GLY|ARG|GLN|ASP|ASN|PRO|TYR|MET|HIS|TRP|CYS|ASX|SEC|XAA|GLX)[0-9]+> 
    <(Leu|Ser|Ile|Glu|Val|Lys|Thr|Phe|Ala|Gly|Arg|Gln|Asp|Asn|Pro|Tyr|Met|His|Trp|Cys|Asx|Sec|Xaa|Glx|LEU|SER|ILE|GLU|VAL|LYS|THR|PHE|ALA|GLY|ARG|GLN|ASP|ASN|PRO|TYR|MET|HIS|TRP|CYS|ASX|SEC|XAA|GLX)>   )
| (  %(DictAminoCode) <[0-9]+> %(DictAminoCode) )
!| ( <(A|B|C|D|E|F|G|H|I|K|L|M|N|P|Q|R|S|T|V|V|W|X|Y|Z)[0-9][0-9][0-9]?(A|B|C|D|E|F|G|H|I|K|L|M|N|P|Q|R|S|T|V|V|W|X|Y|Z)> )
  ![/OD] 
}

#subgroup AAmutation: {
  ![OD AminoAcidAlteration] 
  ( <(p\.)? (A|B|C|D|E|F|G|H|I|K|L|M|N|P|Q|R|S|T|V|V|W|X|Y|Z)[0-9][0-9][0-9]?(A|B|C|D|E|F|G|H|I|K|L|M|N|P|Q|R|S|T|V|V|W|X|Y|Z)> )
| ( <(A|B|C|D|E|F|G|H|I|K|L|M|N|P|Q|R|S|T|V|V|W|X|Y|Z)[0-9][0-9][0-9]?(_|(A|B|C|D|E|F|G|H|I|K|L|M|N|P|Q|R|S|T|V|V|W|X|Y|Z)|[0-9])*((ins|inv|dup|del)(T|A|G|C)*)+> )
  ![/OD] 
}

#subgroup GeneticAlteration: { 
  (
    ( <\(> ( %(DNAnucleotides) | %(AminoAcids) | %(AAmutation) )  <\)> ) 
    | ( %(DNAnucleotides) | %(AminoAcids) | %(AAmutation) )
  )+ 
}

#subgroup Bio_Change: {
  ( [OD Observation_CancerBiomarker_Component_Alteration] [TE OBSERVATION_CANCERBIOMARKER_COMPONENT_ALTERATION] <>+ [/TE] [/OD] )
| %(17pdeletion) | %(OtherDeletion) | %(OtherAlteration) 
}

#subgroup Med_Defect: { 
  ( 
    !( <\(> ( %(Bio_Change) |  %(GeneticAlteration) ) <\)> )
     %(Bio_Change) 
    | %(GeneticAlteration)
    | %(OtherAlteration)
  )+ 
}


!======================================================================
! LOCATION
!======================================================================

#subgroup LocationKeywords: {
  <\p{ci}(((Exon)|(Codon)|(Intron))(s|(e(n?)))?)>
}

!======================================================================

#subgroup Location: {
  ( <\(> %(LocationKeywords) ( <\(> <\p{ci}(s)> <\)> )? ( <\p{ci}(\-|\,|(und)|(oder))>?  <POS:Num> )+ <\)> )
| ( %(LocationKeywords) ( <\(> <\p{ci}(s)> <\)> )? ( <\p{ci}(\-|\,|(und)|(oder))>?  <POS:Num> )+ )
}

!======================================================================

#subgroup BioLocation: {
  [OD Observation_CancerBiomarker_Component_DNARegion] %(Location) ( <(\,|;)>? %(Location) )* [/OD]
}

!======================================================================

!!======================================================================
!! STATUS
!!======================================================================

#subgroup StatusKeywordsNounPos: {
  ( <STEM:Anwesenheit> )
| ( <STEM:((Hin)|(Nach))weis> )
| ( <STEM:Positiv.*, POS:Nn> )
| ( <STEM:Markierung> )
| ( <zahlreiche(n)?> <.*Zellen> )
}

!======================================================================

#subgroup StatusKeywordsNounNeg: {
  ( <STEM:Abwesenheit> )
| ( <fehlende(m|n)?> <STEM:Nachweis> )
| ( <STEM:Negativ.*> )
| ( <Fehlen> )
| ( <keine> <residualen> <Netze> <von> <POS:Adj> <Zellen> )
}

!======================================================================

#subgroup StatusKeywordsNoun: {
 %(StatusKeywordsNounPos)
|%(StatusKeywordsNounNeg)
}

!======================================================================
#subgroup WeaklyPositiveAdj: {
  ( <STEM:schwach> <STEM:positiv> )
| ( <etwas> <STEM:schwach(e)?, POS:Adj> )
| ( <schwach(e)?> <zytoplasmatische> )
| ( <zytoplasmatisch> <pos>? )
}

#subgroup ModeratelyPositiveAdj: {
  ( <STEM:mittelgradige  ,POS:Adj> <bis> <STEM:deutlich ,POS:Adj> )
| ( <mäßiggradig(e)?> | <mässiggradig(e)?> )
| ( <teilweise> | <partiell> )
| ( <sehr>? <STEM:deutlich, POS:Adj> )
| ( <STEM:deutlich> <positiv> )
}

#subgroup UnqualifiedPositiveAdj: {
  ( <STEM:anwesend> )
| ( <STEM:positiv> )
| ( <positiven> )
| ( <\p{ci}((pos)(\.)?)> )
| ( <STEM:kräftig, POS:Adj> )
| ( <STEM:nachweisbar, POS:Adj> )
}

#subgroup StatusKeywordsAdjPosUnmarked:{
  %(WeaklyPositiveAdj) | %(ModeratelyPositiveAdj) | %(UnqualifiedPositiveAdj)
}

#subgroup StatusKeywordsAdjPos: {
!! [OD BiomarkerStatusPositive] %(WeaklyPositiveAdj) | %(ModeratelyPositiveAdj) | %(UnqualifiedPositiveAdj) [/OD]
[OD POS] %(StatusKeywordsAdjPosUnmarked) [/OD]
}

!======================================================================

#subgroup StatusKeywordsAdjNeg: {
  [OD NEG]
  <STEM:(negativ)|(abwesend)>
| <\p{ci}((neg)(\.)?)>
| <\p{ci}(normal)>
| ( <\p{ci}(nicht)> %(Observed) )
| (<wenige>)
  [/OD]
}

!======================================================================

#subgroup StatusKeywordsAdjEqui: {
  [OD IND] 
  (<STEM:fragwürdig, POS:Adj>) 
| (<vereinzel.*>)
| (<STEM:einzeln,POS:Adj>)
| (<STEM:unspezifisch>)
| ( <zytoplasmatisch> <gering> <und> <inkonstant> <positiv> )
  [/OD]
}

!======================================================================

#subgroup StatusKeywordsAdjUnk: {
  [OD IE]
  <\p{ci}(Status)>? <\p{ci}(noch)>? <\p{ci}(nicht)> <\p{ci}(bekannt)>
  [/OD]
}

!======================================================================

#subgroup StatusKeywordsAdj: {
  %(StatusKeywordsAdjPos) 
| %(StatusKeywordsAdjNeg) 
| %(StatusKeywordsAdjEqui) 
| %(StatusKeywordsAdjUnk) 
}

!======================================================================

#subgroup StatusMultiwords: {
  [OD POS] <\p{ci}(.+\-positiv)> [/OD]
| [OD NEG] <\p{ci}(.+\-negativ)> [/OD]
| [OD POS] <\p{ci}(.+\-pos)> [/OD]
| [OD NEG] <\p{ci}(.+\-neg)> [/OD]
| [OD POS] <\p{ci}(.+\-niedrig)> [/OD]
| [OD POS] <\p{ci}(.+\-hoch)> [/OD]
| [OD POS] <\p{ci}(([a-z]|[A-Z]).+\+)> [/OD]
}

!======================================================================

#subgroup StatusPlusorMinus: {
  [OD POS] <\+> [/OD]
!| [OD BiomarkerStatusNegative] <\-> [/OD]
}

!======================================================================
! ALTERATION
!======================================================================

#subgroup DefectAdj: {
  [OD NEG] <\p{ci}(nicht)> <STEM:(mutiert)> [/OD]
| [OD Observation_CancerBiomarker_Component_Alteration] <STEM:(mutiert)> [/OD]
}

#subgroup AllStatusAdj: {
 ( %(DefectAdj)|%(StatusKeywordsAdj) )
}

!! EXAMPLE: "Koexpression von Vimentin und Zytokeratin"
#subgroup MultipleAlterations: {
  [OD Observation_CancerBiomarker_Component_Alteration] [TE OBSERVATION_CANCERBIOMARKER_COMPONENT_ALTERATION] <>+ [/TE] [/OD] %(PrepOF) %(Med_Biomarkers)
}

!======================================================================
! NUMERIC ENTITIES
!======================================================================

!from OOB

#subgroup NumModifier: {
 ( <STEM:(ungefähr)|(höchstens)|(wenigstens)> )
| ( <ca\.> )
| ( <max\.> )
| ( <etwa> )
| ( <bis> )
| ( <über> )
}

#subgroup OtherPercent: {
 <.[0-9]+\%>
|( <[0-9]+> <\%> ) 
}

#subgroup Percent: {
  <\(|\,>? <POS:Prep>? %(NumModifier)? 
  !![OD BiomarkerScore] [TE PERCENT] <>+ [/TE] | %(OtherPercent) [/OD]
    [OD Observation_CancerBiomarker_Value] %(OtherPercent) [/OD]
  <\)>?
}

#subgroup BioNum: {	
  ( <>? <STEM:Messwert> <STEM:von>? )? 
  (
    ( [OD Observation_CancerBiomarker_Value]<[0-9]\.[0-9]> [/OD] )
  | ( [OD Observation_CancerBiomarker_Value]<[0-9]\+[0-9]=[0-9]> [/OD] )
  | ( [OD Observation_CancerBiomarker_Value]<[0-9]\+[0-9]> <=> <[0-9]> [/OD] )
  | ( [OD Observation_CancerBiomarker_Value]<[0-9]> <\+> <[0-9]> (<=> <[0-9]>)? [/OD] )
  | ( [OD Observation_CancerBiomarker_Value]<[0-9]> <\p{ci}(aus)> <[0-9]>[/OD] )
  | ( [OD Observation_CancerBiomarker_Value]<[0-9]\+?> <\p{ci}(bis)> <[0-9]\+?> [/OD] )
  | ( [OD Observation_CancerBiomarker_Value]<[0-9]> <\p{ci}((aus)|(von))> <[0-9]+> [/OD] )
  | ( [OD Observation_CancerBiomarker_Value]<[0-9]\+> [/OD] )
  | ( [OD Observation_CancerBiomarker_Value]<[0-9]> [/OD] )
  )
}

#subgroup BioValue: {
  (<\(> %(BioNum) <\)>)
| (<\,>? %(BioNum))
}

#subgroup BioScore: {
  [OD Observation_CancerBiomarker_Value] 
  ( <\(> <STEM:(Messwert|Score)>? (<0>|<(1|2|3)\+>) ( <\p{ci}(bis)> (<0>|<(1|2|3)\+>) )? <>? <\)> ) 
| ( <STEM:(Messwert|Score)>? (<0>|<(1|2|3)\+>) ( <\p{ci}(bis)> (<0>|<(1|2|3)\+>) )? )
| (<Score> <POS:Num>)
| ( <\+{1,3}> )
| ( <\-{1,3}> )
  [/OD]
}

!======================================================================
! SECTION PROCESSING
!======================================================================

#subgroup BiomarkerSection:{ 
( %(Med_Biomarkers) <\:> )
}

#subgroup BiomarkerSectionNegativeStatus: {
!!  ( %(BiomarkerSection) <>+ [OD NEG] <nicht> [TE Observation_CancerBiomarker_Component_Alteration] <>+ [/TE] [/OD] )
( %(BiomarkerSection) <>+ [OD NEG] <nicht> [/OD] %(Bio_Change) )
| ( %(BiomarkerSection) <>+ %(StatusKeywordsAdjNeg) <Tumorzelle(n)?>?)
| ( %(BiomarkerSection) <>*? <STEM: markieren, POS: V> <>*? [OD NEG] <nicht> [/OD] )
}

#subgroup BiomarkerSectionPositiveStatus: {
 !!%(BiomarkerSection) <>+ %(StatusKeywordsAdjPos) %(StatusKeywordsNounPos)?
( %(BiomarkerSection) <>*? %(StatusKeywordsAdjPos) %(StatusKeywordsNounPos) )
| ( %(BiomarkerSection) <>*? %(StatusKeywordsAdjPos) %(Bio_Change) )
| ( %(BiomarkerSection) <>*? %(StatusKeywordsAdjPos) )
}

#subgroup BiomarkerSectionEquivocalStatus: {
  %(BiomarkerSection) <>*? %(StatusKeywordsAdjEqui) (<>*? <Zellelemente>)?
}

#subgroup BiomarkerSectionScore: {
  ( %(Med_Biomarker) (<\(> <>+ <\)>)? <\:> <>{0,3} <\(> (%(Percent) | %(BioScore)) (<\,> <POS:Adj>)? <\)> )
| ( %(Med_Biomarker) (<\(> <>+ <\)>)? <\:> <POS:~(Punct)>{0,3} (%(Percent) | %(BioScore)) (<\,> <POS:Adj>)? )
| ( %(Med_Biomarker) (<\(> <>+ <\)>)? <\:> %(BioNum) )
}

!===========================================================================
! BIOMARKER GROUP
!===========================================================================

#subgroup Link: <\:|\-> | %(LinkingV)

#subgroup BioLoc: (%(PrepIN)|%(PrepOF)) (%(Med_Biomarker)|%(BioLocation))

#subgroup ByTest: <\p{ci}((durch)|(mit))> %(Med_Test)

#subgroup result: <STEM:((Ergebnis)|(Status))>

#subgroup StatusIndicatorAdjPhrase:{
  ( %(DefectAdj) %(Med_Biomarker) %(Med_Defect)? %(ByTest)* )
| ( %(Med_Biomarkers) %(Med_Defect) %(AllStatusAdj) )
| ( %(AllStatusAdj) %(PrepFOR) %(Med_Defect) %(PrepIN) ( %(BioLocation) | %(Med_Defect) | %(Med_Biomarker) )+ ( %(PrepOF) %(Med_Biomarker) )? )
| ( %(AllStatusAdj) %(PrepFOR) ( %(Med_Defect) | <\p{ci}(Genexpression)> ) %(ByTest) (<\(> %(BioNum) <\)>)? )
| ( %(AllStatusAdj) %(PrepFOR) ( %(BioLocation) | %(Med_Biomarker) )* %(Med_Defect)+ ( %(BioLoc) | %(ByTest))* )
!| ((%(AllStatusAdj) %(PrepFOR))? ( %(BioLocation) | %(Med_Biomarker) )* %(Med_Defect)+ ( %(BioLoc) | %(ByTest))+ )
!| ( (%(AllStatusAdj) %(BioScore) %(PrepFOR) ) %(Med_Biomarker) %(Med_Defect)? %(ByTest)? )
| ( %(AllStatusAdj) %(PrepFOR) %(Med_Biomarker) ( (<\p{ci}(und)>|<\,>)+ %(PrepFOR)? %(Med_Biomarker) )* )
| ( %(AllStatusAdj) %(Med_Test) )
}

#subgroup StatusIndicatorNP:{
  ( [OD POS] %(StatusKeywordsNounPos) [/OD] <>{0,3} %(PrepOF) %(Med_Defect) <im>? %(BioLoc) <POS:Det-Art> %(Med_Biomarker))
| ( [OD POS] %(StatusKeywordsNounPos) [/OD] (%(PrepOF)|%(GenitiveArticle)) [OD Observation_CancerBiomarker_Component_Alteration] <.+\-Mutation> [/OD]  %(BioLoc)? )
| ( [OD NEG] %(StatusKeywordsNounNeg) [/OD] (%(PrepOF)|%(GenitiveArticle)) [OD Observation_CancerBiomarker_Component_Alteration] <.+\-Mutation> [/OD] %(BioLoc)?)
| ( [OD NEG] %(StatusKeywordsNounNeg) [/OD] (%(PrepOF)|%(GenitiveArticle)) ( %(BioLocation) | %(Med_Biomarker) )* <>? %(Med_Defect) %(BioLoc)? )
| ( [OD POS] %(StatusKeywordsNounPos) [/OD] (%(PrepOF)|%(GenitiveArticle)) ( %(BioLocation) | %(Med_Biomarker) )* <>? %(Med_Defect) %(BioLoc)? )
| ( [OD NEG] %(StatusKeywordsNounNeg) [/OD] (%(PrepOF)|%(PrepFOR)|%(GenitiveArticle))  %(Med_Biomarker) ( (<\p{ci}((und)|(oder))>|<\,>)+ %(Med_Biomarker) )* )
| ( <STEM:kein> <>{0,2} [OD NEG] (%(StatusKeywordsNounPos)|<STEM:Positivität>) [/OD] <POS:Prep> %(Med_Biomarker) )
| ( [OD NEG] <STEM:(K|k)ein> [/OD] <>{0,2} %(Med_Biomarker) <>? )
| ( %(StatusKeywordsNounPos) (%(PrepOF)|%(PrepFOR)) %(Med_Biomarkers) %(Link)? <\p{ci}(auch)>? [OD NEG] <\p{ci}(nicht)> %(Observed) [/OD] )
| ( [OD POS] %(StatusKeywordsAdjPosUnmarked)* %(StatusKeywordsNounPos) [/OD] <>{0,5} <POS:Prep> %(Med_Biomarker) )
!| ( <in> [OD NEG] <Abwesenheit> [/OD] <einer> <spezifischen> <Immunreaktion> <gegen> %(Med_Biomarkers) ) 
}

#subgroup StatusIndicatorVP:{
  ( <POS:Det>? %(Med_Biomarkers) (<POS:Aux-Fin>|<POS:V-Fin>|<POS:Modal-Fin>) <>{0,4} %(AllStatusAdj) <POS:V-Inf>? )
| ( <POS:Det>? %(Med_Biomarkers) (<POS:Aux-Fin>|<POS:V-Fin>|<POS:Modal-Fin>) <>{0,4} [OD POS] %(StatusKeywordsNounPos) [/OD] <POS:V-Inf>?)
| ( <POS:Det>? %(Med_Biomarkers) (<POS:Aux-Fin>|<POS:V-Fin>|<POS:Modal-Fin>) <>{0,4} [OD NEG] %(StatusKeywordsNounNeg) [/OD] <POS:V-Inf>?)
}

#subgroup AlterationVP:{
  ( <POS:Det>? %(Med_Biomarkers) (<POS:Aux-Fin>|<POS:V-Fin>) <>{0,4} %(Bio_Change) )
| ( <.*(Z|z)ellen>? %(Bio_Change)  %(AllStatusAdj)? %(Med_Biomarkers) )
| ( %(Bio_Change) (<die>? <.*(Z|z)ellen>)? %(AllStatusAdj)? %(Med_Biomarkers) )
| ( %(Med_Biomarkers) <>{0,3}  [OD Observation_CancerBiomarker_Component_Alteration] <mutiert> [/OD] )
}


#subgroup ScorePhraseNoVerb:{
  ( %(Med_Biomarker) (<Index> <von>)?  (%(BioScore)|%(BioNum)|%(Percent)) )
| ( %(Percent) (%(Med_Biomarker)|( <\(> %(Med_Biomarker) <\)> )))
}

#subgroup ScoreVP:{
 (<POS:Det>? <\(>? %(Med_Biomarkers) <\)>? (<POS:Aux-Fin>|<POS:V-Fin>) <>{0,3} %(Percent) (<der> <Tumorzellkerne>)? %(AllStatusAdj)? )
}

#subgroup Biomarker_Pos_Neg:{
(%(Med_Biomarkers_Pos)|%(Med_Biomarkers_Neg)) <>?
}

#group Observation_CancerBiomarker: {
  ( ( %(Med_Biomarker) %(result)? (<\p{ci}(und)> %(result))? <\:|\-> )? 
      ( ( %(Med_Biomarkers) %(AllStatusAdj) )
    | ( %(Med_Biomarker) %(skipParens)? %(Link)? %(Percent)? %(AllStatusAdj) ( %(BioValue) | %(Percent) )* )
    | ( %(Med_Biomarker) %(skipParens)? %(Link)? %(AllStatusAdj) %(PrepFOR) %(Med_Defect) (%(PrepIN) %(BioLocation))? %(Med_Defect)? )
    | ( %(Med_Biomarker) %(BioScore) %(AllStatusAdj) %(PrepFOR) %(Med_Defect)+ ( %(BioLoc) | %(ByTest))* )
    | %(StatusIndicatorAdjPhrase)
    | %(StatusIndicatorNP)
    | %(StatusIndicatorVP)
    | ( (%(Med_Biomarker)|%(Med_Test)) %(skipParens)? %(Link)? <\p{ci}(auch)>? %(AllStatusAdj)+ (%(PrepFOR) %(Med_Biomarker))? ((<\p{ci}(auch)>|<\,>)+ %(PrepFOR)? %(Med_Biomarker) )*)
    | ( <STEM:kein> %(Med_Biomarker) %(Med_Defect) %(Link) <\p{ci}(auch)>? [OD NEG] %(Observed) [/OD] %(ByTest)? )
    | ( <STEM:kein> %(Med_Defect) %(Link)? <\p{ci}(auch)>? [OD NEG] %(Observed) [/OD]  %(BioLoc)+ %(ByTest)? )
    | ( %(Med_Biomarker) %(Med_Defect)  %(Link) <\p{ci}(auch)>? [OD POS] %(Observed) [/OD] %(ByTest)? )
    | %(AlterationVP)
    | %(ScoreVP)
    | %(ScorePhraseNoVerb)
    ) 
    %(GeneticAlteration)? 
  )
| ( %(Med_Biomarker) %(result)? (<\p{ci}(und)> %(result))? <\:|\-> %(AllStatusAdj) )
| %(StatusMultiwords)
| %(BiomarkerSectionNegativeStatus)
| %(BiomarkerSectionPositiveStatus)
| %(BiomarkerSectionEquivocalStatus)
| %(BiomarkerSectionScore)
| %(Biomarker_Pos_Neg)
| ( %(AllStatusAdj)* %(MultipleAlterations)) 
!| ( %(17pdeletion) | %(OtherDeletion) | %(GeneticAlteration) | %(OtherAlteration) )+
}
