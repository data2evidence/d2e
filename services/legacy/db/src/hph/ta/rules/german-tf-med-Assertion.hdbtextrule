!======================================================================
! File Name: german-tf-med-Assertion.rul 
! Author: ALP Text Analysis
! Project Name: Medical Fact Extraction
! Content: Rules for the extraction of assertions of medical entities, 
!          focusing on polarity, modality, reference and relation
!======================================================================

!======================================================================
! OUTPUT FIELDS
!======================================================================

!! Main entity:		sub-entities

!! Polarity: 			1+ sub-entity PolNegatvie
!! ModalityHypothetical: 	1+ sub-entity ModHypothetical
!! ModalityConditional: 	1+ sub-entity ModConditional
!! ModalityPossible: 		1+ sub-entity ModPossible
!! ModalityProposed: 		1+ sub-entity ModProposed
!! med_ReferenceNotAssociated: 	1+ sub-entity med_RefNotAssociated
!! med_ReferenceFamily: 	1+ sub-entity med_RefFamily
!! med_RelationAllergic: 	1+ sub-entity med_RelAllergic
!! med_RelationIntolerant: 	1+ sub-entity med_RelIntolerant

!======================================================================
!!Entities for clinical reports
!======================================================================

#subgroup Med_Entity: [TE MEDICATIONSTATEMENT_MEDICATION] <>+ [/TE]

#subgroup MedProc_Entity: { ( [TE MEDICATIONSTATEMENT_MEDICATION] <>+ [/TE] 
                            | [TE PROCEDURE_THERAPEUTIC] <>+ [/TE] 
			    | [TE OBSERVATION_LABORATORY] <>+ [/TE] 
			    | [TE PROCEDURE_DIAGNOSTIC] <>+ [/TE] )
			  }

#subgroup Clinical_Entity: { ( [TE CONDITION] <>+ [/TE] 
	                     | [TE OBSERVATION_SYMPTOM] <>+ [/TE] 
			     | [TE PROCEDURE_THERAPEUTIC] <>+ [/TE] 
			     | [TE OBSERVATION_LABORATORY] <>+ [/TE] 
			     | [TE PROCEDURE_DIAGNOSTIC] <>+ [/TE] 
			     | [TE MEDICATIONSTATEMENT_MEDICATION] <>+ [/TE] 
			     | [TE Composition_Section_Problem] <>+ [/TE] )
			   }

#subgroup NotProc_Entity: { ( [TE CONDITION] <>+ [/TE] 
	  		    | [TE OBSERVATION_SYMPTOM] <>+ [/TE] 
			    | [TE MEDICATIONSTATEMENT_MEDICATION] <>+ [/TE] 
			    | [TE Composition_Section_Problem] <>+ [/TE] )
			  }

#subgroup DisSymp_Entity: { ( [TE CONDITION] <>+ [/TE] 
	                    | [TE OBSERVATION_SYMPTOM] <>+ [/TE] 
			    | [TE Composition_Section_Problem] <>+ [/TE] )
			  }

!======================================================================
!copied here from pathology rules
!These are key words that can trigger a rule, but are too generic to get a "histology" label
#subgroup CancerGeneric: {
  ( <STEM:((Krebs)|(Tumor))> )
| ( <Wachstum> )
| ( <Neoplasie> )
| ( <Tumorerkrankung(en)?> )
| ( <Malignit.*> )
| ( <Zellelement(e)?> )
| ( <Atypie(n)?> )
| ( <STEM:.*(K|k)arzinose> )
| ( <STEM:.*(F|f)ilia(e)?> )
| ( <STEM:.*(M|m)etastase(n)?> )
| ( <Malignom(s)?> )
}

#subgroup CancerRelatedAdjs:{
  ( <primäre(n)?> )
| ( <sekundäre(n)?> )
| ( <ausmessbare(r)?> )
| ( <atypische(n)?> )
| ( <zellul.re(n)?> )
| ( <> <behandelte(s|n|m|r)?> )
}

!======================================================================
!!Entities for pathology reports
!======================================================================

#subgroup Histology_Entity:  [TE OBSERVATION_CANCERHISTOLOGY_VALUE] <>+ [/TE]
#subgroup Anatomy_Entity:    [TE OBSERVATION_CANCERHISTOLOGY_BODYSITE] <>+ [/TE]
#subgroup Procedure_Entity:  [TE PROCEDURE] <>+ [/TE]
#subgroup Alteration_Entity: [TE OBSERVATION_CANCERBIOMARKER_COMPONENT_ALTERATION] <>+ [/TE]
#subgroup Behavior_Entity:   [TE OBSERVATION_CANCERHISTOLOGY_COMPONENT_BEHAVIOR] <>+ [/TE]
#subgroup HistoType_Entity:  [TE HISTOTYPE] <>+ [/TE]
#subgroup GradeType_Entity:  [TE OBSERVATION_CANCERGRADE_NORMALIZED] <>+ [/TE]
#subgroup GradeValue_Entity: [TE OBSERVATION_CANCERGRADE_VALUE] <>+ [/TE]
#subgroup Margin_Entity:     [TE MARGIN] <>+ [/TE]
#subgroup Appearance_Entity: [TE APPEARANCE] <>+ [/TE]
#subgroup Modifier_Entity:   [TE MODIFIER] <>+ [/TE]
#subgroup Biomarker_Entity:  [TE OBSERVATION_CANCERBIOMARKER_NORMALIZED] <>+ [/TE]
#subgroup med_Path_Entity:   [TE OBSERVATION_CANCERHISTOLOGY_VALUE_ICDO] <>+ [/TE]

#subgroup Pathology_Entity: { %(Histology_Entity) 
	                    | %(Procedure_Entity) 
			    | %(Alteration_Entity) 
			    | %(Behavior_Entity) 
			    | %(HistoType_Entity) 
			    | %(GradeType_Entity) 
			    | %(GradeValue_Entity) 
			    | %(Margin_Entity) 
			    | %(Appearance_Entity) 
			    | %(Modifier_Entity) 
			    | %(Biomarker_Entity) 
			    | %(med_Path_Entity) 
			    | %(CancerGeneric) 
			    }

!======================================================================
!!All entities coming from dictionaries
!======================================================================

#subgroup Dict_Entity: %(Clinical_Entity) | %(Pathology_Entity)

!======================================================================
!!coordination
!======================================================================

#subgroup coord: <\p{ci}((und)|(sowie)|&|(oder)|/|(\|))> | (<\,> <\p{ci}(und|&|oder)>?) | (<als> <auch>)

!======================================================================
!!"filler" (tokens you can skip over more-or-less safely)
!======================================================================

#subgroup filler: <POS:~(V|Aux|Modal|Conj-Subord|Pron-Rel|Punct)>

!======================================================================
!!negation keywords (stolen from German VOC - modify this for medical domain)
!======================================================================

#subgroup not: {
  (<\p{ci} (nicht)> <\p{ci} (einmal|mehr)>? <\p{ci}((so)|(derartig)|(gerade))>?) 
| <\p{ci} (niemals)> | <\p{ci} (nie)> | <\p{ci} (nimmer)> 
| (<\p{ci} (nie)> <\p{ci} (und)> <\p{ci} (nimmer)>) 
| <\p{ci} (keinesfalls)> | <\p{ci} (keineswegs)> | <\p{ci} (mitnichten)> 
| (<\p{ci} (auf)> <\p{ci} (keinen)> <\p{ci} (Fall)>) 
| (<\p{ci} (in)> (<\p{ci} (keiner)>|<\p{ci} (keinster)>) <\p{ci} (Weise)>) 
| (<\p{ci} (nicht)> <\p{ci} (im)> (<\p{ci} (Geringsten)> | <\p{ci} (Mindesten)>)) 
| ((<\p{ci} (kein)> | <\p{ci} (nicht)> <\p{ci} (ein)>) <\p{ci} (bisschen)>)
| (<\p{ci}(alles)> <\p{ci}(andere)> <\p{ci}(als)>) 
| <\p{ci} (nichts)> | <\p{ci}(nix)> | <\p{ci} (garni(s)?cht)> | <\p{ci}(ni(s)?cht)>
| <\p{ci} (weder)> | (<\p{ci}(von)> <\p{ci}(wegen)>)
| <STEM:minimal> ! das geht minimal zu Lasten der Bildqualität
| (<\p{ci}(nicht)> <\p{ci}(ein(s|(e((m|n|r|s)?))))>) ! nicht einer ist beschädigt worden
| <STEM:wenig, POS:Det/Pron> | (<\p{ci}(am)> <\p{ci}(wenigsten)>)
| <\p{ci}(nichtmal)>
}
#subgroup no: {
  <STEM:kein(e)?|keinerlei> | ((<(O|o)hne, POS:Prep>|%(not)) <STEM:(.+ - (kein.*)), POS:Det|Det/Pron>?)
| (<\p{ci}(von)> <\p{ci}(wegen)> <POS:Det-Art>?) ! can also be used to negate adjectives, and after the noun - just leave it here for now
| <\p{ci} (weder)> 
| <STEM:gering|minimal|niedrig, POS:Adj-Attr> ! an experiment: "geringes Rauschen" ~= "kein Rauschen"
}

#subgroup NotNo: %(not) | %(no)

!============================================
! HELPER SUBGROUPS FOR ANATOMICAL MENTIONS
!=============================================
#subgroup AnatomyGeneric:{
   <(Z|C)ervix> <uteri>?
}

#subgroup AnatomySpecifier:{
  ( <(der)|(des)> (%(Anatomy_Entity)|%(AnatomyGeneric)) )

}

!=============================================
! HELPER SUBGROUPS FOR LITERAL CODE EXTRACTION
!=============================================

#subgroup ICDO3Header:{
 ( <ICD\-O(\-(Nr\.))?> (<M>? <\:>)? )
|( <Klassifikation> <\:> <ICD> <\-> <O> <\-> <M> <\?>? )
}


!======================================================================
! NEGATIVE POLARITY
!======================================================================

#subgroup PolarityNEGATIVEPath: {
   !![OD PolNegative] %(CancerRelatedAdjs)* %(Pathology_Entity)+ [/OD]
 !!| [OD PolNegative] %(Pathology_Entity)+ %(AnatomySpecifier)+ [/OD]
 !!| [OD PolNegative] %(CancerRelatedAdjs)* %(Pathology_Entity){1,2} %(AnatomySpecifier) [/OD]
  ( %(CancerRelatedAdjs)* [OD Negative] %(Pathology_Entity)+ [/OD] %(AnatomySpecifier)* )
}

#subgroup PolarityNEGATIVE: {
  ( [OD Negative] %(Clinical_Entity) [/OD] (%(coord) %(filler){0,2} [OD Negative] %(Clinical_Entity) [/OD])* )
| %(PolarityNEGATIVEPath) 
}
#subgroup PolarityNEGATIVENotProc: {
  ( [OD Negative] %(NotProc_Entity) [/OD] (%(coord) %(filler){0,2} [OD Negative] %(NotProc_Entity) [/OD])* )
| %(PolarityNEGATIVEPath)  
}
#subgroup PolarityNEGATIVEMedProc: {
  [OD Negative] %(MedProc_Entity) [/OD] (%(coord) %(filler){0,2} [OD Negative] %(MedProc_Entity) [/OD])*
}

#subgroup Exclusion: {
  ( <\p{ci}(ausgenommen)> )
| ( (<\p{ci}(mit)> <der>)? <Ausnahme> <(des)|(der)|(eines)|(einer)>? )
| ( <\p{ci}(mit)> <Ausnahme> <von>? )
| ( <\p{ci}(au((ß)|(ss))er)> )
| ( <AUSSER> <im> <Falle> <von> )
| ( <\p{ci}(abgesehen)> <von> )
| ( <Ausnahme> <\:> )
| ( <Ausnahmen> <sind> ) 
}


#subgroup LackOfEvidence: {
!! <STEM:kein> ((<STEM:Hinweis|Nachweis> <STEM:auf>?)|(<STEM:Anhalt> <STEM:für>)) <STEM:ein>?
  ( <STEM:kein> (<STEM:Hinweis|Nachweis> (<STEM:(auf)|(von)|(für)> <>*?)) )
| ( <STEM:kein> <STEM:Anhalt> <für> )
| ( <keine> <typische> <Immunkonstellation> <für> <>? )
  
}

#subgroup NegativeDeterminerPhrases: {
 (<STEM:(K|k)ein(e)?> <POS:Adj>{0,3} %(PolarityNEGATIVE))
|(<STEM:(K|k)ein(e)?> <POS:Adj>{0,2} <Zyklus> %(PolarityNEGATIVE))
}

#subgroup NegativeAdjectives:{
  ( <nicht> <POS:Adj> ) 
}

#subgroup NegativeVerb:{
 ( <STEM:verneinen> )
|( <STEM:ablehnen> )
}

#subgroup NegVerbPastPart:{
 ( <STEM:verneinen, POS:V-PaPart> )
|( <STEM:ablehnen, POS:V-PaPart> )
}

#subgroup NegativeVerbPhrase:{
  ( <POS:V-Fin> <sich> <nicht> )
| ( <POS:Aux-Fin> <nicht> <zu> <POS:V-Inf> )
| ( <POS:Aux-Fin> <nicht> <POS:V-PaPart> )
| ( <POS:Aux-Fin> <nicht> <nachweisbar> )
| ( <POS:Aux-Fin> %(NegativeAdjectives) )
| ( <STEM:werden> <>{0,4} %(NegativeVerb) )
| ( <>{0,4} %(NegVerbPastPart) ) 
}

#subgroup DictICDO3LiteralPolNegative:{
( [OD Negative] [TE OBSERVATION_CANCERHISTOLOGY_VALUE_ICDO] <M?[0-9]+.?[0-9]A>+ [/TE]+ [/OD] )
}

#subgroup ICDO3PolNegLiterals:{
( %(ICDO3Header) %(DictICDO3LiteralPolNegative) (%(coord)? %(DictICDO3LiteralPolNegative))* )
}

#group polarity: {
  ( %(LackOfEvidence) %(PolarityNEGATIVE) )
| ( <ohne> <>{0,3} %(PolarityNEGATIVE) )
| ( %(Exclusion) %(PolarityNEGATIVE) (%(coord) <>{0,2} %(PolarityNEGATIVE))+ )
| ( %(NegativeDeterminerPhrases) )
| ( %(PolarityNEGATIVE) (<\(> <>{0,4} <\)>)? %(NegativeVerbPhrase) )
| ( %(PolarityNEGATIVE) %(NegativeAdjectives) )
| %(ICDO3PolNegLiterals)

! %(NotNo) <>{0,3} %(PolarityNEGATIVE)
}

!========================================================================
! HYPOTHETICAL MODALITY
!========================================================================

#subgroup ModalityHYPOTHETICAL: {
  ( [OD Provisional] %(Clinical_Entity) [/OD] (%(coord) <>{0,2} [OD Provisional] %(Clinical_Entity) [/OD])* )
| ( [OD Provisional] %(Pathology_Entity) [/OD] ) 
}
#subgroup ModalityHYPOTHETICALDisSymp: {
  ( [OD Provisional] %(DisSymp_Entity) [/OD] (%(coord) <>{0,2} [OD Provisional] %(DisSymp_Entity) [/OD])* )
| ( [OD Provisional] %(Pathology_Entity) [/OD] ) 
}

#subgroup ModalityHypothetical: {
!!  <\p{ci}((wenn)|(falls)|(ggf\.))> <>{0,8} %(ModalityHYPOTHETICAL)
  ( <\p{ci}((wenn)|(falls))> <>*? %(ModalityHYPOTHETICAL) <>*? <POS:((V-Fin)|(Aux-Fin))> )
| ( ((<(i|I)m> <Fall(e)?>)|<ggf\.>) <>*? %(ModalityHYPOTHETICAL) )
}

!========================================================================
! CONDITIONAL MODALITY
!========================================================================

#subgroup ModalityCONDITIONALDisSympOne: {
  [OD Provisional] %(Pathology_Entity) [/OD]
}

#subgroup ModalityCONDITIONALDisSymp: {
  ( [OD Provisional] %(DisSymp_Entity) [/OD] (%(coord) <>{0,2} [OD Provisional] %(DisSymp_Entity) [/OD])* )
| ( [OD Provisional] %(Pathology_Entity) [/OD] ) 
}

#subgroup ModalityConditional: {
 (<STEM:verursachen, POS:V> <~(\,|;|\(|\)|(durch))>{0,2} %(ModalityCONDITIONALDisSymp))
|(%(ModalityCONDITIONALDisSymp) <nach> <>{0,4} <Treppensteigen>)
}

!======================================================================
! POSSIBLE MODALITY
!======================================================================

#subgroup ModalityPOSSIBLE: {
  ( [OD Provisional] %(Clinical_Entity) [/OD] (%(coord) <>{0,2} [OD Provisional] %(Clinical_Entity) [/OD])* )
| ( [OD Provisional] %(Pathology_Entity)+ [/OD] (%(coord) <>{0,2} [OD Provisional] %(Pathology_Entity) [/OD])* )
| ( [OD Provisional] %(Pathology_Entity)+ [/OD] ( <\(> <z\.> <B\.> [OD Provisional] %(Pathology_Entity)+ [/OD] <\)> ) ) 
}

#subgroup ModalityPOSSIBLEDisSymp: {
  ( [OD Provisional] %(DisSymp_Entity) [/OD] (%(coord) <>{0,2} [OD Provisional] %(DisSymp_Entity) [/OD])* )
| ( [OD Provisional] %(Pathology_Entity) [/OD] ) 
}

#subgroup PossibleDisSymp_Verbs: {
  <STEM:vermuten, POS:V>
| <STEM:ausgehen, POS:V>
| <STEM:handeln, POS:V>
}

#subgroup PossibleDisSymp_Ads: {
  <STEM:zweifelhaft>
}

#subgroup PossibleDisSymp_Nouns: {
  <STEM:Verdacht>
}

#subgroup PossibleDisSympKeywords: {
  %(PossibleDisSymp_Verbs)
| %(PossibleDisSymp_Ads)
| %(PossibleDisSymp_Nouns)
}

#subgroup PossibleAdjectives: {
  <STEM:möglich>
!!| <STEM:.*suspekte(n|m|r)?, POS:Adj>
| <STEM:.*verdächtig(en)?>
| <STEM:vermutlich>
| <STEM:fraglich>
| <STEM:potenziell>
}

#subgroup Possible_Ads_Neg: {
  <STEM:unmöglich>
}

#subgroup Possible_Nouns: {
  <STEM:Möglichkeit>
}

#subgroup PossibleAdverbs:{
  <am> <ehesten>
| <vielleicht>
| <möglicherweise>
}

#subgroup PossibleKeywords: {
  %(PossibleAdjectives)
| %(Possible_Nouns)
| %(PossibleAdverbs)
}


#subgroup PossiblePrepPhrases: {
  ( <V\.a\.> )
| ((<V(\.)?> <\.>?) (<a(\.)?>  <\.>?))
| ( <STEM:Verdacht> <STEM:auf> )
| ( <verdächtig> <auf> )
| ( (<Ver(d)?(\.)?> <\.>?)  (<a(\.)?> <\.>?) )
| ( <STEM:kein> <STEM:typisch> <POS:Nn> <POS:Prep> )
| ( <nicht> <unbedingt> <typisch> <für> )
| ( <vereinbar> <mit> )
| ( <zur> <Prävention> <eine(r|s)> )
| ( <zum> <Ausschlu((ss)|(ß))> )
| ( <z\.> <B\.> )
| ( <passend> <zu> )
}


#subgroup PossiblePhrasesVerbSecondPos: {
  ( (<es>? <ist>)|(<ist> <es>? )) <von>
| ( (<es>? <müsste>)|(<müsste> (<es> <sich>?)? ))
| ( <wäre(n)?> <hier>? )
| ( <könnte(n)?> )
| ( <pass((en)|(t))> )
| ( <(sprechen)|(spricht)> <>{0,5}(<in> <1\.> <Linie>)? <für> )
| ( (<(d|D)er> <Befund>)? <lässt> <in> <erster> <Linie> )
| ( <STEM:kommen> <in> (<1\.>|<erster>) <Linie> )
}

#subgroup PossiblePhrasesVerbLastPos: {
  ( <so>? <POS:Conj-Subord> <>{0,4} %(ModalityPOSSIBLE) %(PossibleKeywords) <POS:Aux-Fin> )
| ( (<POS:Pron-Rel>|<POS:Pron-Int/Rel>)?  <POS:Prep> <>{0,3} %(ModalityPOSSIBLE) <(sprechen)|(spricht)> )
| ( (<POS:Pron-Rel>|<POS:Pron-Int/Rel>)?  <POS:Prep> <>{0,3} %(ModalityPOSSIBLE) <ungewöhnlich> <wäre(n)?> )
| ( (<POS:Pron-Rel>|<POS:Pron-Int/Rel>)? <>*? %(ModalityPOSSIBLE) <hochgradig>? <suspekt> <(ist)|(sind)> )
| ( %(ModalityPOSSIBLE) <zu> <rechnen> <ist> )
| ( <POS:Prep> %(ModalityPOSSIBLE) <zu> <vereinbaren> ) 
}

#subgroup DictICDO3LiteralModPossible:{
( [OD Provisional] [TE OBSERVATION_CANCERHISTOLOGY_VALUE_ICDO] <M?[0-9]+.?[0-9]V>+ [/TE]+ [/OD] )
}

#subgroup ICDO3ModPosLiterals:{
( %(ICDO3Header) %(DictICDO3LiteralModPossible) (%(coord)? %(DictICDO3LiteralModPossible))* )
}

!!#subgroup PossibleKeywords: {
!!  %(PossibleAdjectives)
!!| %(Possible_Nouns)
!!| %(PossibleAdverbs)
!!}

#subgroup ModalityPossible: {
  (%(PossibleDisSympKeywords) <~(\,|;|\(|\))>{0,4} %(ModalityPOSSIBLEDisSymp))
| (%(PossibleKeywords) <~(\,|;|\(|\)|\.)>{0,6} %(ModalityPOSSIBLE))
| (%(PossiblePhrasesVerbSecondPos) (%(PossibleAdverbs)? <~(\.)>*? %(ModalityPOSSIBLE))+ (<zu>? <POS:V>)? )
| (<komm((en)|(t))> <>*? %(ModalityPOSSIBLE) <in> <Betracht>)
| (%(PossiblePrepPhrases) <>*? %(ModalityPOSSIBLE))
| (%(PossiblePhrasesVerbLastPos))
| (%(PossibleAdjectives) %(ModalityPOSSIBLE))
| (%(ModalityPOSSIBLE) <\?>)
| %(ICDO3ModPosLiterals)
}


!======================================================================
! PROPOSED MODALITY
!======================================================================

#subgroup ModalityPROPOSED: {
  [OD Preparation] %(MedProc_Entity)|%(Procedure_Entity) [/OD] <POS:Adj-Pred>? ((%(coord)|<vor>) <>{0,4} [OD Preparation] (%(MedProc_Entity)|%(Procedure_Entity)) [/OD])*
| [OD Preparation] %(Pathology_Entity)+ [/OD] 
}

#subgroup Proposed_Verbs: {
  <(W|w)ir>? <STEM:(vorschlagen)|(raten)|(empfehlen)|(bitten)|(planen), POS:V>
}

#subgroup Proposed_Modals: {
  <STEM:(sollen)|(dürfen), POS:Modal>
}

#subgroup ProposedAdjectives:{
  <STEM:.*bedürftig, POS:Adj>
}

#subgroup Proposed_Ads: {
  ( <\p{ci}(passend)> <\p{ci}(zu)> )
| ( <erforderlich> )
}

#subgroup Proposed_Nouns: {
  <STEM:(Rat)|(.*(V|v)orschlag), POS:Nn>
| (<STEM:Empfehlung> (<zu(r|m)?> |<\:>))
| (<STEM:Bitte> <um>)
}

#subgroup ProposedPrepPhrases:{
  (<((A|a)uf)|((B|b)ei)>  <STEM:Wunsch>) 
}

#subgroup ProposedKeywords: {
  %(Proposed_Verbs)
| %(ProposedAdjectives)
| %(Proposed_Nouns)
| %(ProposedPrepPhrases)
}

#subgroup ModalityProposed: {
  ( %(ProposedKeywords) <~(\,|;|\(|\))>{0,6} %(ModalityPROPOSED ))
| ( %(ModalityPROPOSED) %(Proposed_Nouns ))
| ( %(Proposed_Modals) <~(\,|;|\(|\)), POS:~(V)>{0,2} %(ModalityPROPOSED) (<>{0,7} <POS:((Aux-Inf)|(V-Inf))>)? )
| ( %(ModalityPROPOSED) %(Proposed_Modals) <>{0,5} <werden >)
| ( <per>? %(ModalityPROPOSED) <>{0,5} <werden> %(Proposed_Modals) )
| ( <(ist)|(sind)> <>{0,2} %(ModalityPROPOSED) <>{0,2} %(Proposed_Ads) )
| ( <(ist)|(sind)> <>{0,5} %(ModalityPROPOSED) <>{0,2} %(Proposed_Verbs) )
}

!=============================
!STATUS
!=============================

#group status: {
%(ModalityPossible)
|%(ModalityHypothetical)
|%(ModalityConditional)
|%(ModalityProposed)
}


!========================================================================
! FAMILY REFERENCE
!========================================================================

#subgroup ReferenceFAMILY: {
  [OD FamilyMemberHistory] %(Clinical_Entity) [/OD] (%(coord) <>{0,2} [OD FamilyMemberHistory] %(Clinical_Entity) [/OD])*
}

#subgroup FamilyNoun: {
  ( <STEM:(Familie)|(Bruder)|(Schwester)|(Vater)|(Mutter)> )
| ( <Familienanamnese> <\:> )
}

#subgroup FamilyNouns: {
  %(FamilyNoun) (%(coord) <>{0,2} %(FamilyNoun))*
}

#subgroup FamilyAdj: {
  <STEM:(mütterlich)|(väterlich)>
}

#subgroup med_ReferenceFamily: {
  (<\p{ci}(starb)> <\p{ci}(an)> <>{0,2} %(ReferenceFAMILY))
| (%(FamilyNouns) <>{0,4} %(ReferenceFAMILY))
| (<STEM:Kind> <~(\,|;|\(|\))>{0,4} %(ReferenceFAMILY))
| (%(FamilyAdj) <POS:~(Punct)>{0,2} %(ReferenceFAMILY))
}

!======================================================================
! NOT ASSOCIATED REFERENCE
!======================================================================

#subgroup ReferenceNOTASSOCIATED: {
  ( [OD Other] %(Clinical_Entity) [/OD] (%(coord) <>{0,2} [OD Other] %(Clinical_Entity) [/OD])* )
!!| ( [OD Other] %(CancerRelatedAdjs)* %(Pathology_Entity)+ [/OD] )
| ( %(CancerRelatedAdjs)* [OD Other] %(Pathology_Entity)+ [/OD] ) 
}

#subgroup NotassociatedKeywords_Verbs: {
  <STEM:((besprechen)|(lesen)|(diskutieren)), POS:V>
}

#subgroup PersonGeneric: {
  ( <Frauen> )
| ( <Männer> )
| ( <Patienten> )
| ( <Patientinnen> )
}

#subgroup NotassociatedKeywords_Nouns: {
  <STEM:((Information)|(Buch)), POS:Nn> <POS:Prep>
}

#subgroup NotassociatedKeywords: {
  %(NotassociatedKeywords_Verbs)
| %(NotassociatedKeywords_Nouns)
}

#subgroup StudiesReference: {
  ( %(PersonGeneric) <mit> <>*? %(ReferenceNOTASSOCIATED) )
| ( %(PersonGeneric) <\,> <die> <>*? %(ReferenceNOTASSOCIATED) <>*? <POS:V> <POS:Aux>? )
!!| (  <STEM:Diagnose> <ein((es)|(er))> <>*? %(ReferenceNOTASSOCIATED) (%(coord) <>*? %(ReferenceNOTASSOCIATED))* )
}

#subgroup ProfessionalGeneric:{
  <Kollegen>
}

#subgroup DepartmentReference:{
  %(ProfessionalGeneric) <(der)|(des)> %(ReferenceNOTASSOCIATED)
}

#subgroup DisclaimerReference:{
  ( %(ReferenceNOTASSOCIATED) <(der)|(des)> <jeweiligen> <Patient((en)|(in))> )
}

#subgroup med_ReferenceNotAssociated: {
  ( %(NotassociatedKeywords) <~(\,|;|\(|\))>{0,4} %(ReferenceNOTASSOCIATED) )
| ( %(StudiesReference) )
| ( %(DepartmentReference) )
| ( %(DisclaimerReference) )
}

!===================================
! SUBJECT
!===================================

#group subject:{
%(med_ReferenceFamily)
|%(med_ReferenceNotAssociated)
}

!======================================================================
! ALLERGIC RELATION
!======================================================================

#subgroup RelationALLERGICMed: {
  [OD AllergyIntolerance_Reaction_Substance] %(Med_Entity) [/OD] (%(coord) <>{0,2} [OD AllergyIntolerance_Reaction_Substance] %(Med_Entity) [/OD])*
}

#subgroup AllergicKeywords: {
 (<STEM:allergisch> <STEM:Reaktion>?)
|(<Allergien> <\:>)
}

#group AllergyIntolerance_Allergy: {
 (%(AllergicKeywords) <>{0,3} %(RelationALLERGICMed))
|(%(RelationALLERGICMed) <>{0,5} %(AllergicKeywords)) 
}

!======================================================================
! INTOLERANT RELATION
!======================================================================

#subgroup RelationINTOLERANTMedProc: {
  [OD AllergyIntolerance_Reaction_Substance] %(MedProc_Entity) [/OD] (%(coord) <>{0,2} [OD AllergyIntolerance_Reaction_Substance] %(MedProc_Entity) [/OD])*
}

#subgroup Intolerant_Ads: {
  <STEM:unverträglich>
}

#subgroup Intolerant_Verbs: {
  <\p{ci}(nicht)> <STEM:vertragen, POS:V>
}

#subgroup Intolerant_Nouns:{
  <STEM:schlecht> <STEM:Verträglichkeit>
}

#subgroup IntolerantKeywords: {
  %(Intolerant_Ads) | %(Intolerant_Verbs) | %(Intolerant_Nouns)
}

#group AllergyIntolerance_Intolerance: {
  (<POS:Prep>? %(IntolerantKeywords) <>{0,3} %(RelationINTOLERANTMedProc))
| (%(RelationINTOLERANTMedProc) <>{0,5} %(IntolerantKeywords))
}


