!======================================================================
! File Name: german-tf-med-Pathology.rul 
! Author: ALP Text Analysis
! Project Name: Medical Fact Extraction
! Content: Rules for the extraction of pathology report specific entities
! Encoding: cp-1252
!======================================================================
!======================================================================
!! PATHOLOGY REPORTS:
!======================================================================
   
!======================================================================
! OUTPUT FIELDS:
!======================================================================
! Main Cancer group: med_CancerRelation

! Obligatory subentity:
!!	CancerHistology


!Optional subentities:
!!	CancerBehavior
!!	GradeType
!!	GradeValue
!!	CancerSite
!!	CancerCellType
!!	CancerHistologicGrade
!!	CancerHistologicType


!======================================================================
! Metastasis group (TNM): !!med_Metastasis

! Obligatory subentity:
!!	CancerMetastasis

!======================================================================
! Lymph Nodes group (TNM): !!med_Lymph_nodes

! Obligatory subentity:
!!	CancerNodes

!======================================================================
! Stage group (TNM): !!med_Stage

!! Cancer stage refers to the extent or severity of the cancer, based on factors 
!! such as the location of the primary tumor, tumor size, number of tumors, and 
!! lymph node involvement (spread of cancer into lymph nodes)

! Obligatory subentity:
!!	CancerStage

!======================================================================
! Tumor group (TNM): !!med_Tumor

! Obligatory subentity:
!!	CancerTumor

!======================================================================
! Tumor Size group: med_TumorSize

! Obligatory subentity:
!!	CancerTumorSize


!======================================================================
! Procedure group: med_Procedure (not being exposed yet)

! Obligatory subentity:
!!	CancerProcedure


!======================================================================
! Tumor Grade group: med_TumorGrade

!! Tumor grade is a system used to classify cancer cells in terms of how abnormal 
!! they look under a microscope and how quickly the tumor is likely to grow and 
!! spread. 
!! Histologic grade, also called differentiation, refers to how much the tumor 
!! cells resemble normal cells of the same tissue type. 
!! Nuclear grade refers to the size and shape of the nucleus in tumor cells and 
!! the percentage of tumor cells that are dividing.

! Obligatory subentity:
!!	GradeValue

!Optional subentities:
!!	GradeType

!======================================================================
!! Common defines:
!======================================================================

#subgroup skipParens: <\(><~(\()>+<\)>

#subgroup coord: <\p{ci}((und)|&|(oder)|/|(\|))> | (<\,> <\p{ci}(und|&|oder)>?)

#subgroup neg: {
  <nicht>
| <STEM:kein>
}

#subgroup modifier:{ 
  (<POS:Adj>)
| (<POS:Num>)
| (<STEM:(kein(e|(er)|(em)|(en))?)>)
} 

#subgroup positionAdverb: {
  (<rechts>) 
| (<links>)
| ( <\(> <rechts> <\)> )
| ( <\(> <links> <\)> ) 
}

#subgroup positionAdjective: {
   (<STEM:recht, POS:Adj>)
|  (<STEM:link, POS:Adj>)
|  (<STEM:peritumoralen, POS:Adj>)
|  (<re.?> <>?)
}


!======================================================================
!!Entities coming from dictionaries
!======================================================================
!! APPEARANCE
!! BEHAVIOR
!! GRADE
!! HISTOLOGY
!! LYMPH_NODES
!! ANATOMY
!! PRECANCER - "pre-cancer"   In the TNM classification, carcinoma in situ is reported as TisN0M0 (Stage 0)
!! PROCEDURE

#subgroup DictAppearance:   [OD CancerCellType] [TE APPEARANCE] <>+ [/TE] <STEM:Typ>? [/OD]
#subgroup DictBehavior:     [OD Observation_CancerHistology_Component_Behavior] [TE OBSERVATION_CANCERHISTOLOGY_COMPONENT_BEHAVIOR] <>+ [/TE] [/OD] ( %(coord) [OD Observation_CancerHistology_Component_Behavior] [TE OBSERVATION_CANCERHISTOLOGY_COMPONENT_BEHAVIOR] <>+ [/TE] [/OD] )?
#subgroup DictGradeValue:   [TE OBSERVATION_CANCERGRADE_VALUE] <>+ [/TE] 
#subgroup DictGradeType:    [TE OBSERVATION_CANCERGRADE_NORMALIZED] <>+ [/TE] 
!!#subgroup DictHistology:    [OD CancerHistology] [TE HISTOLOGY] <>+ [/TE] | <(.*(K|k)arzinom(a|s|en)?)> [/OD]
#subgroup DictHistology:    [OD Observation_CancerHistology_Value] [TE OBSERVATION_CANCERHISTOLOGY_VALUE] <>+ [/TE] [/OD]
#subgroup DictLymph_nodes:  [TE LYMPH_NODES] <>+ [/TE] 
#subgroup Laterality:       <STEM:lateral>
#subgroup DictAnatomy:      %(Laterality)? [TE OBSERVATION_CANCERHISTOLOGY_BODYSITE] <>+ [/TE]
#subgroup GeneralProcedure: <STEM:Dissektion>
#subgroup DictProcedure:    [TE PROCEDURE] <>+ [/TE]
#subgroup DictType:         [OD CancerHistologicType] [TE HISTOTYPE] <>+ [/TE]+ [/OD]
#subgroup DictOtherMod:     [OD CancerModifier] [TE MODIFIER] <>+ [/TE] [/OD]
#subgroup DictICDO:         [OD Observation_CancerHistology_Value_ICDO] [TE OBSERVATION_CANCERHISTOLOGY_VALUE_ICDO] <([a-zA-Z]|(ä|Ä)|(ö|Ö)|(ü|Ü)|(ß))+>+ [/TE]+ [/OD]
#subgroup DictICDO3Literal: [OD Observation_CancerHistology_Value_ICDO][TE OBSERVATION_CANCERHISTOLOGY_VALUE_ICDO] <M?[0-9]+.?[0-9](A|V)?>+ [/TE]+ [/OD]

!======================================================================
!These are key words that can trigger a rule, but are too generic to get a "histology" label
!======================================================================

#subgroup CancerGeneric: <STEM:((Tumor)|(Krebs))(zell)?>

!======================================================================

#subgroup CancerType: {
  %(DictHistology)
| %(DictICDO)
!!| %(CancerGeneric)
}

!======================================================================
!! EXAMPLE:  "0.04 cm"            						
!! EXAMPLE:  "0.2 mm"            						
!! EXAMPLE:  "0.4 cm."            						
!! EXAMPLE:  "1 CM"            						    
!! EXAMPLE:  "1.0 mm"            						
!! EXAMPLE:  "1.1 cm."            						
!! EXAMPLE:  "30 cm."            						
!! EXAMPLE:  "6.0 mm"            						
!! EXAMPLE:  "7.0 cm."            						

#subgroup Measure: { 
  [OD Observation_CancerSize_Value]
  ( <POS:Num> ( <\.> | <\->| <x|X> ) <POS:Num>+ <(C|c|M|m)(M|m)\.?> ) 
| ( <POS:Num> <(C|c|M|m)(M|m)\.?> ) 
| ( <[0-9]+(\.|\-|\,)[0-9](C|c|M|m)(M|m)\.?> ) 
| ( <[0-9]+(C|c|M|m)(M|m)\.?> )
  [/OD]
}

!======================================================================
!! med_Lymph_nodes, med_Metastasis and med_Tumor are all being output as part of med_Stage (as per Style Guide)
!======================================================================
!! EXAMPLE:  "pT1a"
!! EXAMPLE:  "pT1b"
!! EXAMPLE:  "pT2"
!! EXAMPLE:  "pTis"
!! EXAMPLE:  "Tis"
!! EXAMPLE:  "ypT0"

#subgroup Tumor: { 
  [OD Observation_CancerStage_Component_T]
  <(y?r?p)T(0|1|2|3|4|X|x|i)[a-z]{0,2}(\(m\))?>
| <T(0|1|2|3|4)[a-z]{0,2}(\(m\))?>
| <(y?p)?T\(m\)([0-9]|X|x|i)[a-z]{0,2}>
| <PT(0|1|2|3|4)[a-z](\(m\))?>
| <(y?p)?T(0|1|2|3|4)[A-Z]{1,2}>
| <(y?p)?Tis>
| <pTa>
| <c?[0-9]?T(0|1|2|3|4)[a-z]?>
  [/OD] 
}

!======================================================================
!! EXAMPLE:   "N0(i-)(sn)"
!! EXAMPLE:   "pN0"
!! EXAMPLE:   "pN0(i-)"
!! EXAMPLE:   "pN0(i+)"
!! EXAMPLE:   "pN1a(sn)"
!! EXAMPLE:   "pN0 (sn)"
!! EXAMPLE:   "pNX"
!! EXAMPLE:   "ypN0"



!!!#subgroup Lymph_nodes: {
#subgroup N_Score: { 
  [OD Observation_CancerStage_Component_N]
  <(y?p)?N(0|1|2|3|4|X|x)[a-z]{0,2}(\((sn|i\+|i\-)\))+>   ! pT1b N0(i-)(sn)
| ( <(y?p)?N(0|1|2|3|4|X|x)[a-z]{0,2}(\((sn|i\+|i\-)\))*> (<\(> (<sn>|(<i><\+|\->)) <\)>)* )  ! pT1c, pN0 (sn)
| ( <POS:Num> <N(0|1|2|3)> )
| ( <LN> <STEM:(positiv)|(negativ)> )
| (<c?[0-9]?N(0|1|2|3)[a-z]?>)
  [/OD] 
}

!=====================================================================

#subgroup LymphNodesExamined: {
  <\(> [OD LN_Afflicted_Over_Examined] <POS:Num> [/OD] <\)>
}

!====================================================================

#subgroup Lymph_nodes:{
!! %(N_Score) %(LymphNodesExamined)?
%(N_Score)
}

!=====================================================================

#subgroup LymphVesselScores:{
 [OD L_Classification] <L(0|1)?> [/OD]  
}

!======================================================================
!! EXAMPLE:  "cM0"
!! EXAMPLE:  "M0"
!! EXAMPLE:  "M1b"
!! EXAMPLE:  "Mx"
!! EXAMPLE:  "pMX"

#subgroup Metastasis: {
  [OD Observation_CancerStage_Component_M]
  <(c|p)?M(0|1|X|x)[a-z]{0,2}>
| <(c|p)?M\+> | <(c|p)?M\->
| ( <M> <STEM:(((an)|(ab))(wesend))> )
| ( <\p{ci}(negativ)> <\p{ci}(für)> <\p{ci}(M(a|i)krometastase)> )
| ( <STEM:kein> (<STEM:Hinweis> <\p{ci}(auf)>)? <\p{ci}(M(a|i)krometastase)> )
  [/OD] 
}


!======================================================================
!! EXAMPLE:   "pT3bpN0"
!! EXAMPLE:   "Stage 0"
!! EXAMPLE:   "stage I"
!! EXAMPLE:   "Stage IA"
!! EXAMPLE:   "Stage IIIB"
!! EXAMPLE:   "Stage IIIC1"
!! EXAMPLE:   "ypTisN0"

#subgroup Stage: {
  <(y?p)?T(0|1|2|3|4|X|x|i)[a-z]{0,2}(\/|\,)?p?N(0|1|2|3|4|X|x)[a-z]{0,2}((\/|\,)?(c|p)?M(0|1|X|x)[a-z]{0,2})?>
| ( <\p{ci}((Tumor)?Stadium)> <((0|1|2|3|4)[a-z]?|(I|X|V)+([a-z]|[A-Z])?)> )
| ( <\p{ci}((Tumor)?Stadium)> <I(a|A)> )
| ( <\p{ci}((Tumor)?Stadium)> <(I|X|V)+[a-zA-Z][0-9]> )
}

!======================================================================
#subgroup GradeNum: {
  <(1|2|3|4|(i|v|I|V|x|X)+)>
| <[0-9]\-[0-9]>
| <[0-9]\/[0-9]>
| <[0-9]\+[0-9]>
| <[0-9]\+[0-9]=[0-9]>
| <[0-9]\+[0-9]\+[0-9]=[0-9]>
| ( <[0-9]\+[0-9]> <=> <[0-9]> )
| ( <[0-9]\+[0-9]> <\(> <\p{ci}(summe=[0-9])> <\)> )
| ( <[0-9]> <\+> <[0-9]> (<=> <[0-9]>)? )
| ( <[0-9]> <\p{ci}((aus)|(von))> <[0-9]> )
| ( <[0-9]\+?> <\p{ci}(bis)> <[0-9]\+?> )
| ( <[0-9]> <\p{ci}((aus)|(von))> <[0-9]+> )
| <(I|V)+\/(I|V)+>
| <(I|V)+\-(I|V)+>
| ( <(I|V)+> <\p{ci}((aus)|(von))> <(I|V)+> )
| <(I|V){1,3}(A|B|C)(1|2)?>
| ( <STEM:Summe> <=>? <[0-9]> )
| <[0-9]\([0-9]\+[0-9]\)>
| <[0-9]a?> 
}

!======================================================================

#subgroup GradeEquals: <\p{ci}(=|\:|\-|(ist)|(von)|(gleich))>

!======================================================================
!! EXAMPLE:  "(1-2)"            						
!! EXAMPLE:  "(1 of 3)"            						
!! EXAMPLE:  "1/3"            						    
!! EXAMPLE:  "2-3"            						    
!! EXAMPLE:  "2 + 3 (SUM 5)"            				
!! EXAMPLE:  "2 + 3"            						
!! EXAMPLE:  "2+3, SUM 5"            					
!! EXAMPLE:  "3 + 2 (SUM 5)"            				
!! EXAMPLE:  "3 + 4 (SUM = 7)"            				
!! EXAMPLE:  "3 + 4 equals 7"            				
!! EXAMPLE:  "3/3"            						    
!! EXAMPLE:  "3/9"            						    
!! EXAMPLE:  "3+3 (SUM 6)"            					
!! EXAMPLE:  "3+3 = 6"            						
!! EXAMPLE:  "6(3+3)"            						
!! EXAMPLE:  "9 (4+5)"            						
!! EXAMPLE:  "9 (5 + 4)"            					
!! EXAMPLE:  "9 (5+4)"            						
!! EXAMPLE:  "I OF III"            						
!! EXAMPLE:  "I/III"            						
!! EXAMPLE:  "II (7/9)"            						
!! EXAMPLE:  "II/III"            						
!! EXAMPLE:  "III of III"            					
!! EXAMPLE:  "Intermediate (2 of 3)"            		

#subgroup GradeValue: {
  ( <\(> %(GradeNum) <\)> )
| %(GradeNum) 
| %(DictGradeValue) 
| <GS?(1|2|3|4|X)>
| <G(1|2|3|4)\-[0-9]>
| <GRADE(1|2|3|4)>
| <grade(1|2|3|4)>
| <Grade(1|2|3|4)>
| ( <G(1|2|3|4)> <\p{ci}((aus)|(von))> <[0-9]> )
}

!======================================================================

#subgroup Differentiation: {
((<POS:Adj>|<POS:Adv>) <differenzierte(n|s)?>)
}

!======================================================================

#subgroup GradeModifier: {
  ( <\(> [OD Observation_CancerGrade_Value] %(DictGradeValue) [/OD] <\)> )
| [OD Observation_CancerGrade_Value] %(DictGradeValue) [/OD]
!! | [OD GradeValue] %(Differentiation) [/OD] 
}

!======================================================================
!! EXAMPLE:  "BLOOM-RICHARDSON"            				
!! EXAMPLE:  "Gleason's score"            				
!! EXAMPLE:  "Gleason Histologic Score"            		
!! EXAMPLE:  "GLEASON PATTERN"            				
!! EXAMPLE:  "GS"            						    
!! EXAMPLE:  "Histologic grade (FIGO I-III)"            
!! EXAMPLE:  "HISTOLOGIC GRADE (FIGO)"            		
!! EXAMPLE:  "Modified Bloom-Richardson score"          
!! EXAMPLE:  "NOTTINGHAM GRADE (GI-III)"            	
!! EXAMPLE:  "Nuclear Grade"            				
!! EXAMPLE:  "Primary Gleason Pattern"            		
!! EXAMPLE:  "Secondary Gleason Pattern"            	
!! EXAMPLE:  "Total Gleason Score"            			
!! EXAMPLE:  "Tubular differentiation score"            
!! EXAMPLE:  "tubule score"            					

#subgroup GradeType:{ 
 %(DictGradeType)
}

!======================================================================

#subgroup TumorGrade: {
  (
    ( [OD Observation_CancerGrade_Normalized] %(GradeType) %(GradeValue) <\+> %(GradeType) %(GradeValue)[/OD] %(GradeEquals) [OD Observation_CancerGrade_Value] <\p{ci}(Wert)> %(GradeValue)+ [/OD] )
  | ( [OD Observation_CancerGrade_Normalized] %(GradeType) <[0-9]\+> %(GradeType) %(GradeValue)[/OD] %(GradeEquals) [OD Observation_CancerGrade_Value] <\p{ci}(Wert)>  %(GradeValue)+ [/OD] )
  | ( [OD Observation_CancerGrade_Normalized] %(GradeType)[/OD] %(GradeEquals)? ( <STEM:(Grad)|(Wert)> %(GradeEquals)? )? [OD Observation_CancerGrade_Value] %(GradeValue)+ (<\,|;>? <\p{ci}((Summe)|(gleich))> <[0-9]>)? [/OD] )
  | ( [OD Observation_CancerGrade_Normalized] %(GradeType)[/OD] %(GradeEquals)? [OD Observation_CancerGrade_Value] %(GradeValue) <\p{ci}(bis)> %(GradeValue) ( <\p{ci}((Summe)|(gleich))> <[0-9]> )? [/OD] )
  | ( [OD Observation_CancerGrade_Normalized] %(GradeType)[/OD] %(GradeEquals) %(GradeModifier) )
  | ( [OD Observation_CancerGrade_Normalized] %(GradeType)[/OD] %(GradeEquals) [OD Observation_CancerGrade_Value] <STEM:(hoch)|(niedrig)> %(skipParens)? [/OD] )
  | ( [OD Observation_CancerGrade_Normalized] %(GradeType)[/OD] %(GradeEquals) [OD Observation_CancerGrade_Value] <STEM:(hoch)|(niedrig)> <\,> %(GradeValue) [/OD] )
  | ( %(GradeValue) <\,> %(GradeModifier) )
!!  | ( <G(1|2|3)> )
  |  ( [OD Observation_CancerGrade_Value] <G(1|2|3)> [/OD] ( <\(> <nach> [OD Observation_CancerGrade_Normalized] %(GradeType) [/OD] <\)> )? )
  | ( %(GradeModifier) )
  ) 
  ( <\,|\:>? ( %(GradeModifier) | ( <\(> <STEM:(hoch)|(niedrig)> <\)> ) ) )?
| ( [OD Observation_CancerGrade_Normalized] %(GradeType) [/OD] <\:> [OD Observation_CancerGrade_Value] %(GradeValue) [/OD] )
}

!======================================================================

#subgroup DictCancerSite: [OD Observation_CancerHistology_BodySite] %(DictAnatomy) [/OD]

#subgroup CancerMod: {
  %(DictCancerSite)
| %(DictAppearance)
| %(DictBehavior)
| %(DictOtherMod)
| ( %(DictType) ( <\(> %(DictType) <\)> )? )
}

!======================================================================

!!EXAMPLE: "Tumoren der Ewing -Sarkome-Familie"

#subgroup TumorFamily:{
<STEM:Tumor> <der> <>*? <.*(F|f)amilie>
}

#subgroup HistologyPrepositionalPhrase: {
  (<in> <Segment> <POS:Num>)
| (<im> <Bereich> (<der>|<des>) [OD Observation_CancerHistology_BodySite] %(DictAnatomy) [/OD])
| (((<im>)|(<in> <POS:Det>)) %(positionAdjective)? [OD Observation_CancerHistology_BodySite] %(DictAnatomy) [/OD])
| (<mit> %(positionAdjective)? %(DictOtherMod))
| (<von> (<der>|<den>|<dem>) <POS:Adj>{0,3} [OD Observation_CancerHistology_BodySite] %(DictAnatomy)[/OD])
| (<vom> <POS:Adj>{0,3} [OD Observation_CancerHistology_BodySite] %(DictAnatomy) [/OD])
| (<vom> <>{0,3} <Typ(\.)?>) 
}

#subgroup HistologyGenitiveExpression: {
  ((<der>)|(<des>)) <POS:Adj>{0,3} [OD Observation_CancerHistology_BodySite] %(DictAnatomy) [/OD] %(positionAdverb)?
}

#subgroup HistologyPosition:{
  %(positionAdjective) [OD Observation_CancerHistology_BodySite] %(DictAnatomy) [/OD]
}

#subgroup HistologyPhrase: {
  %(HistologyPrepositionalPhrase)
| %(HistologyGenitiveExpression)
| %(HistologyPosition)
}

#subgroup HistAdjOther:{
!!  ([OD GradeValue] %(DictGradeValue) [/OD])
!!| (<> <(de)?differenziert.*> )
  (%(GradeModifier))
| (<> <gebaut.*> )
| ( (%(DictOtherMod)|<>)  <lo(c|k)alisiert.*>)
}

#subgroup HistologyDescriptors:{
( (<partiell>|<teils>|<((vor)|(über))wiegend>)? (%(DictBehavior) | %(DictType) | %(DictOtherMod)| %(HistAdjOther)) (<\,>|(<als> <auch>)|<und>)? )
| (<von> (<der>|<dem>) [OD Observation_CancerHistology_BodySite] %(DictAnatomy) [/OD] <ausgehend((en)|(e)|(es)|(em))>)
}

#subgroup ICDO3Header:{
 ( <ICD\-O(\-(Nr\.))?> (<M>? <\:>)? )
|( <Klassifikation> <\:> <ICD> <\-> <O> <\-> <M> <\?>? )
}

#subgroup ICDO3HistologyLiteral:{
 (%(ICDO3Header) %(DictICDO3Literal) (%(coord) %(DictICDO3Literal))* )
}

#subgroup HistologyLiteral:{
%(ICDO3HistologyLiteral)
}

!!EXAMPLE: "Plattenepithelkarzinom vom Rücken"
!!EXAMPLE: "Carcinoma in situ"
!!EXAMPLE: "nicht invasiven Urothelkarzinom"
!!EXAMPLE: "invasiven, duktalen Mammakarzinom"

#group Observation_CancerHistology: {
  ( %(HistologyDescriptors)+ %(CancerType) %(DictBehavior)*  (%(coord)? %(HistologyPhrase))+ )
| ( %(HistologyDescriptors)+ %(CancerType) %(DictBehavior)* %(positionAdverb) (%(coord) %(positionAdverb))? )
| ( %(HistologyDescriptors)+ %(CancerType) %(DictBehavior)* ) 
| ( %(CancerType) %(DictBehavior)* %(HistologyPhrase) (%(coord)? %(HistologyPhrase))* )
| ( %(CancerType) %(DictBehavior)* %(positionAdverb) (%(coord) %(positionAdverb))? )
| ( %(CancerType) %(DictOtherMod)? (%(DictBehavior) | %(DictType))?)
!!| %(TumorFamily)
| %(HistologyLiteral)
| %(DictHistology)
| %(DictICDO) 
}						 

!======================================================================

!!EXAMPLE: "G3"
!!EXAMPLE: "mittelgradig differenziertes"
!!EXAMPLE: "Bloom und Richardson score 3+2+1=6" 

#group Observation_CancerGrade: %(TumorGrade)	 

!======================================================================

#subgroup SizeKeyword: <STEM:(Größe)|(Länge)>

#subgroup SizeKeywords: {
  %(SizeKeyword) 
  (   (<\/|\:> <POS:~(Punct)>? %(SizeKeyword)) 
    | (<\(> <POS:~(Punct)>? %(SizeKeyword) <\)>) 
  )?
}

#subgroup NumModifier: {
  <STEM:((ungefähr)|(wenisgtens)|(höchstens)|(aktuell))>
| <(C|c)irca>
| (<(B|b)is> <zu>)
| <(M|m)aximal>
| <max\.>
| <knapp>
}

#subgroup SizePredicate: {
 ( %(skipParens)? ( <STEM:sein, POS:Aux>|<\:|\,|\(> )? %(NumModifier)? %(Measure) )
|( <STEM:messen> %(NumModifier)? %(Measure) )
|( <>{0,4} <(TD)|(Tumordicke)> %(Measure) )
}

#subgroup CancerSizeMod: {
  %(DictAppearance)
| %(DictBehavior)
| %(DictOtherMod)
| %(DictGradeValue)
| ( %(DictType) ( <\(> %(DictType)  <\)> )? )
}

#subgroup CancerSizePostpositiveMod:{
   ( %(DictBehavior) )
 | ( <POS:Prep> <POS:Det>? [OD Observation_CancerHistology_BodySite] %(DictAnatomy) [/OD] )
}


!!EXAMPLE: "Bis zu 2,1 cm großes plattenepitheliales Carcinoma"
!!EXAMPLE: "circa 0,6 cm messenden, oberflächlich ulcerierten, soliden Basalzellkarzinom"  

#group Observation_CancerSize: {
  ( %(CancerMod)* %(CancerType) %(HistologyPhrase)? <\,>? %(SizeKeywords)? %(SizePredicate) )
| ( %(CancerSizeMod)+ %(CancerType)? <\,>? %(SizeKeywords)? %(SizePredicate) )
| ( %(SizeKeywords) <STEM:((aus)|(von))> <POS:~(Punct)>{0,3} %(CancerType) %(SizePredicate) )
| ( %(CancerSizeMod) <STEM:((aus)|(von))> %(SizePredicate)) 
| ( %(NumModifier)? %(Measure) (<.*mess.*>|(<mess> <\.>))?  (<\,>? ((%(CancerSizeMod)|<POS:Adj>|<POS:Adv>) <POS:V-Fin>?))* %(CancerType) %(CancerSizePostpositiveMod)? ) 
}						 

!======================================================================

#subgroup TNM_Main: {
 (%(Metastasis)|%(Lymph_nodes)|%(Tumor))
}

#subgroup TNM_Secondary: {
 %(LymphVesselScores)
}

#subgroup TNM_Score: {
!! (%(TNM_Main)|%(TNM_Secondary))
%(TNM_Main)
}

#group Observation_CancerStage: {
  [OD Observation_CancerStage_Value_TNMComplete] %(Stage) [/OD]
| ( %(TNM_Score) (<\,>? %(TNM_Score))* )
!!| ( (%(Metastasis)|%(Lymph_nodes)|%(Tumor)) (<\,>? (%(Metastasis)|%(Lymph_nodes)|%(Tumor)))* )
}


!======================================================================

#subgroup PrimaryKeyword:{
 <STEM:Primärort>
}

#subgroup PrimaryAdjective:{
 <primäre(s|n)?>
}

#subgroup PrimaryVerbPastPart:{
 <erstdiagnostiziert>
}

#subgroup DictPrimarySite: {
  [OD PrimarySite] [TE OBSERVATION_CANCERHISTOLOGY_BODYSITE] <>+ [/TE] [/OD] (%(coord) [OD PrimarySite] [TE OBSERVATION_CANCERHISTOLOGY_BODYSITE] <>+ [/TE] [/OD])*
}

!! #group med_CancerPrimary: {
!!   ( %(PrimaryKeyword) <~((und)|(wenn)|\,|;|\(|\))>{0,3} %(DictPrimarySite) ) 
!! | ( %(DictPrimarySite) <~((und)|(wenn)|\,|;|\(|\))>{0,2} %(PrimaryKeyword) )
!! | ( %(PrimaryAdjective) %(DictHistology) <>*? %(DictPrimarySite) )
!! }


